import jakt::path { Path }
import jakt::platform { platform_module, platform_errno }
import platform_errno() { errno_value, erange_value }
import jakt::platform::utility { allocate, null }

import extern c "sys/stat.h" {
    extern fn mkdir(anon pathname: raw c_char, anon mode: u32) -> c_int
}

import extern c "unistd.h" {
    extern fn getcwd(anon mut buf: raw c_char, anon size: usize) -> raw c_char
}

import extern c "stdlib.h" {
    extern fn realpath(anon path: raw c_char, anon mut resolved_path: raw c_char) -> raw c_char
}

import extern "dirent.h" {
    extern struct DIR {}
    extern struct dirent {
        d_name: raw c_char
        d_type: u32
    }

    extern fn opendir(anon pathname: raw c_char) -> raw DIR
    extern fn readdir(anon dir_fd: raw DIR) -> raw dirent
    extern fn closedir(anon dir_fd: raw DIR) -> c_int
}

fn make_directory(path: String) throws {
    let rc = mkdir(path.c_string(), mode: 0o777)
    if rc == 0 {
        return
    }

    throw Error::from_errno(errno_value())
}

fn current_directory() throws -> String {
    // Initial guess at path length, resembles PATH_MAX and the page-size of some systems
    mut buffer_size : usize = 4096;
    loop {
        mut buffer = allocate<c_char>(count: buffer_size)
        defer {
            unsafe { cpp { "free(buffer);" } }
        }

        let buf = getcwd(buffer, buffer_size)
        if buf == null<raw c_char>() {
            let getcwd_errno = errno_value()
            if getcwd_errno == erange_value() {
                // grow buffer and try again
                buffer_size *= 2
                continue
            } else {
               throw Error::from_errno(getcwd_errno)
            }
        }

        mut b = StringBuilder::create()
        b.append_c_string(buf)
        return b.to_string()
    }
}

fn real_path(anon path: String) throws -> String {
    let buffer = realpath(path: path.c_string(), resolved_path: null<raw c_char>())
    if buffer == null<raw c_char>() {
        throw Error::from_errno(errno_value())
    }
    defer {
        unsafe { cpp { "free(buffer);" } }
    }

    mut b = StringBuilder::create()
    b.append_c_string(buffer)
    return b.to_string()
}

class DirectoryIterator implements(ThrowingIterable<(Path, bool)>) {
    path: Path
    dir_fd: raw DIR

    public fn next(mut this) throws -> (Path, bool)? {
        let file = readdir(.dir_fd)

        if file == null<raw dirent>() {
            closedir(.dir_fd)

            return None
        }

        mut builder = StringBuilder::create()
        mut is_directory = false

        unsafe {
            builder.append_c_string((*file).d_name)

            // FIXME: Extern constant support
            cpp {
                "if ((*file).d_type == DT_DIR)"
                "is_directory = true;"
            }
        }

        let filename = builder.to_string()
        if filename == "." or filename == ".." {
            return .next()
        }

        return (Path::from_string(filename), is_directory)
    }

    public fn get_path(this) -> Path {
        return .path
    }

    public fn from_path(path: Path) throws -> DirectoryIterator? {
        let dir_fd = opendir(path.to_string().c_string())
        if dir_fd == null<raw DIR>() {
            return None
        }

        return DirectoryIterator(path, dir_fd)
    }
}

trait FSPath {
    fn absolute(this) throws -> Path
}

type Path implements(FSPath) {
    fn absolute(this) throws -> Path {
        return Path::from_string(real_path(.to_string()))
    }
}
