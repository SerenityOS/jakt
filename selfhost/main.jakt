/// Expect:
/// - output: ""

// Copyright (c) 2022, JT <jt@serenityos.org>
// Copyright (c) 2022, Andreas Kling <kling@serenityos.org>
//
// SPDX-License-Identifier: BSD-2-Clause

import compiler { Compiler, FileId }
import codegen { CodeGenerator }
import error { JaktError, print_error }
import formatter { Formatter }
import utility { FilePath, ArgsParser, Span, escape_for_quotes }
import lexer { Lexer }
import parser { Parser }
import interpreter { Interpreter, InterpreterScope, value_to_checked_expression }
import typechecker { Typechecker }
import types { FunctionId, ResolvedNamespace, ScopeId, ModuleId, Value, ValueImpl }
import repl { REPL, serialize_ast_node }
import ide

function usage() => "usage: jakt [-h] [OPTIONS] <filename>"
function help() -> String {
    mut output = "Flags:\n"
    output += "  -h,--help\t\t\t\tPrint this help and exit.\n"
    output += "  -O\t\t\t\t\tBuild an optimized executable.\n"
    output += "  -dl\t\t\t\t\tPrint debug info for the lexer.\n"
    output += "  -dp\t\t\t\t\tPrint debug info for the parser.\n"
    output += "  -dt\t\t\t\t\tPrint debug info for the typechecker.\n"
    output += "  -S\t\t\t\t\tOnly output source (do not build).\n"
    output += "  -cr, --compile-run\t\t\tBuild and run an executable file.\n"
    output += "  -r, --run\t\t\t\tRun the given file without compiling it (all positional arguments after the file name will be passed to main).\n"
    output += "  -d\t\t\t\t\tInsert debug statement spans in generated C++ code.\n"
    output += "  --debug-print\t\t\t\tOutput debug print.\n"
    output += "  -p --prettify-cpp-source\t\tRun emitted C++ source through clang-format.\n"
    output += "  -S,--emit-cpp-source-only\t\tWrite the C++ source to file, even when not building/\n"
    output += "  -c,--check-only\t\t\tOnly check the code for errors.\n"
    output += "  -j,--json-errors\t\t\tEmit machine-readable (JSON) errors.\n"
    output += "  -H,--type-hints\t\t\tEmit machine-readable type hints (for IDE integration).\n"
    output += "  --try-hints\t\t\t\tEmit machine-readable try hints (for IDE integration).\n"
    output += "  --repl\t\t\t\tStart a Read-Eval-Print loop session.\n"


    output += "\nOptions:\n"
    output += "  -F,--clang-format-path PATH\t\tPath to clang-format executable.\n\t\t\t\t\tDefaults to clang-format\n"
    output += "  -D,--dot-clang-format-path PATH\tPath to the .clang-format file to use.\n\t\t\t\t\tDefaults to none, invoking clangs default .clang-format file handling.\n"
    output += "  -R,--runtime-path PATH\t\tPath of the Jakt runtime headers.\n\t\t\t\t\tDefaults to $PWD/runtime.\n"
    output += "  -B,--binary-dir PATH\t\t\tOutput directory for compiled files.\n\t\t\t\t\tDefaults to $PWD/build.\n"
    output += "  -C,--cxx-compiler-path PATH\t\tPath of the C++ compiler to use when compiling the generated sources.\n\t\t\t\t\tDefaults to clang++.\n"
    output += "  -I PATH\t\t\t\tAdd PATH to compiler's include list. Can be specified multiple times.\n"
    output += "  -L PATH\t\t\t\tAdd PATH to linker's search list. Can be specified multiple times.\n"
    output += "  -l,--link-with LIB\t\t\tLink executable with LIB. Can be specified multiple times.\n"
    output += "  -o,--output-filename FILE\t\tName of the output C++ file and binary.\n\t\t\t\t\tDefaults to the input-filename.\n"
    output += "  -g,--goto-def INDEX\t\t\tReturn the span for the definition at index.\n"
    output += "  -t,--goto-type-def INDEX\t\tReturn the span for the type definition at index.\n"
    output += "  -e,--hover INDEX\t\t\tReturn the type of element at index.\n"
    output += "  -m,--completions INDEX\t\tReturn dot completions at index.\n"
    return output
}

function indent(anon level: usize) throws -> String {
    mut output = ""
    for i in 0uz..level {
        output += "    "
    }
    return output
}

struct FormatRange {
    start: usize
    end: usize
}
function parse_format_range(anon range: String, anon input_file_length: usize) throws -> FormatRange? {
    // range: <start-offset>:<end-offset>
    // {start,end}-offset: usize

    let parts = range.split(':')
    if parts.is_empty() {
        return FormatRange(
            start: 0
            end: input_file_length
        )
    }

    let start_input = parts[0].to_uint()
    if not start_input.has_value() {
        return None
    }

    let start = start_input! as! usize
    let end = match parts.size() {
        1 => input_file_length
        2 => {
            let end_input = parts[1].to_uint()
            if not end_input.has_value() {
                return None
            }

            yield end_input! as! usize
        }
        else => {
            return None
        }
    }

    return FormatRange(
        start
        end
    )
}

function main(args: [String]) {
    if args.size() <= 1 {
        eprintln("{}", usage())
        return 1
    }

    mut args_parser = ArgsParser::from_args(args)
    
    if args_parser.flag(["-h", "--help"]) {
        println("{}\n", usage())
        println("{}", help())
        return 0
    }

    if args_parser.flag(["-v", "--version"]) {
        println("unreleased")
        return 0
    }

    let optimize = args_parser.flag(["-O"])
    let lexer_debug = args_parser.flag(["-dl"])
    let parser_debug = args_parser.flag(["-dp"])
    let typechecker_debug = args_parser.flag(["-dt"])
    let build_executable = not args_parser.flag(["-S"])
    let run_executable = args_parser.flag(["-cr", "--compile-run"])
    let codegen_debug = args_parser.flag(["-d"])
    let debug_print = args_parser.flag(["--debug-print"])
    let prettify_cpp_source = args_parser.flag(["-p", "--prettify-cpp-source"])
    let json_errors = args_parser.flag(["-j","--json-errors"])
    let dump_type_hints = args_parser.flag(["-H", "--type-hints"])
    let dump_try_hints = args_parser.flag(["--try-hints"])
    let check_only = args_parser.flag(["-c", "--check-only"])
    let write_source_to_file = args_parser.flag(["-S", "--emit-cpp-source-only"])

    let clang_format_path = args_parser.option(["-F", "--clang-format-path"]) ?? "clang-format"
    let runtime_path = args_parser.option(["-R", "--runtime-path"]) ?? "runtime"
    let binary_dir = args_parser.option(["-B", "--binary-dir"]) ?? "build"
    let dot_clang_format_path = args_parser.option(["-D", "--dot-clang-format-path"])
    let cxx_compiler_path = args_parser.option(["-C", "--cxx-compiler-path"]) ?? "clang++"
    let extra_include_paths = args_parser.option_multiple(["-I"])
    let extra_lib_paths = args_parser.option_multiple(["-L"])
    let extra_link_libs = args_parser.option_multiple(["-l"])
    let set_output_filename = args_parser.option(["-o", "--output-filename"])
    let goto_def = args_parser.option(["-g", "--goto-def"])
    let goto_type_def = args_parser.option(["-t", "--goto-type-def"])
    let hover = args_parser.option(["-e", "--hover"])
    let completions = args_parser.option(["-m", "--completions"])

    let interpret_run = args_parser.flag(["-r", "--run"])

    let format = args_parser.flag(["-f", "--format"])
    let input_format_range = args_parser.option(["-fr", "--format-range"]) ?? ""

    if args_parser.flag(["--repl"]) {
        mut repl = REPL::create()
        repl.run()
        return 0
    }

    let positional_arguments = args_parser.remaining_arguments()

    mut file_name: String? = None
    mut first_arg = true
    mut interpreted_main_arguments: [String] = []

    for arg in positional_arguments.iterator() {
        if first_arg {
            first_arg = false
            continue
        }

        if not file_name.has_value() {
            file_name = arg
            continue
        }

        if not interpret_run {
            eprintln(
                "Extra unknown argument '{}', you can only pass one source file (was '{}')"
                arg
                file_name
            )
            eprintln("{}", usage())
            return 1
        }

        interpreted_main_arguments.push(arg)
    }
    if not file_name.has_value() {
        eprintln("you must pass a source file")
        eprintln("{}", usage())
        return 1
    }

    let file_path = FilePath::make(file_name!)
    // if file_path.ext() != "jakt" {
    //     eprintln("the compiler expects files with file extension .jakt")
    //     eprintln("{}", usage())
    //     return 1
    // }

    mut errors: [JaktError] = []

    mut compiler = Compiler(
        files: []
        file_ids: [:]
        errors: []
        current_file: None
        current_file_contents: []
        dump_lexer: lexer_debug
        dump_parser: parser_debug
        ignore_parser_errors: false
        debug_print: debug_print
        include_paths: extra_include_paths
        json_errors
        dump_type_hints
        dump_try_hints
    )

    compiler.load_prelude()

    let main_file_id = compiler.get_file_id_or_register(file_path)
    let file_is_set = compiler.set_current_file(main_file_id)
    if not file_is_set {
        return 1
    }

    let tokens = Lexer::lex(compiler)

    if lexer_debug {
        for token in tokens.iterator() {
            println("token: {}", token)
        }
    }

    let format_range = parse_format_range(
        range: input_format_range
        input_file_length: compiler.current_file_contents.size()
    )
    if not format_range.has_value() {
        eprintln("invalid format range '{}', expected <start>(:<end>?)", input_format_range)
        return 1
    }

    if format {
        mut on_new_line = true
        for formatted_line in Formatter::for_tokens(tokens) {
            for formatted_token in formatted_line.iterator() {
                if not formatted_token.token.span().is_in_offset_range(start: format_range!.start, end: format_range!.end) {
                    continue
                }

                for byte in formatted_token.preceding_trivia.iterator() {
                    print("{:c}", byte)
                }

                match formatted_token.token {
                    Eol(comment) => {
                        if comment.has_value() {
                            mut next_char = b' '
                            if comment!.length() != 0 {
                                next_char = comment!.byte_at(0)
                            }
                            let space = match next_char {
                                b' ' | b'\t' | b'/' => ""
                                else => " "
                            }
                            let lhs_space = match on_new_line {
                                true => indent(formatted_token.indent)
                                else => " "
                            }

                            print("{}//{}{}", lhs_space, space, comment!)
                        }
                        on_new_line = true
                    }
                    else => {
                        if on_new_line {
                            // println("(indent: {})", formatted_token.indent)
                            print(indent(formatted_token.indent))
                        }
                        print("{}", formatted_token.token_text())
                        on_new_line = false
                    }
                }

                for byte in formatted_token.trailing_trivia.iterator() {
                    print("{:c}", byte)
                }

                if formatted_token.token is Eol {
                    println()
                }
            }
        }
        return 0
    }

    let parsed_namespace = Parser::parse(compiler, tokens)

    if parser_debug {
        println("{:#}", parsed_namespace);
    }

    let checked_program = Typechecker::typecheck(
        compiler
        parsed_namespace
    )

    if interpret_run {
        mut interpreter = Interpreter::create(
            compiler
            program: checked_program
            spans: []
        )

        // Find the main function
        let prelude_scope_id = ScopeId(module_id: ModuleId(id: 0), id: 0)
        mut main_function_id: FunctionId? = None
        for module in checked_program.modules.iterator() {
            for scope in module.scopes.iterator() {
                if not (scope.parent?.equals(prelude_scope_id) ?? false) {
                    continue
                }

                for function_ in scope.functions.iterator() {
                    if function_.0 == "main" {
                        main_function_id = function_.1
                        break
                    }
                }

                if main_function_id.has_value() {
                    break
                }
            }
            if main_function_id.has_value() {
                break
            }
        }

        if not main_function_id.has_value() {
            eprintln("Error: No main function found in program")
            return 1
        }

        let namespace_: [ResolvedNamespace] = []
        let call_span = Span(file_id: FileId(id: 0), start: 0, end: 0)
        let first_main_param = checked_program.get_function(main_function_id!).params.first()
        let arguments = match first_main_param.has_value() {
            true => {
                mut passed_arguments: [Value] = [Value(impl: ValueImpl::JaktString(file_name!), span: call_span)]
                for argument in interpreted_main_arguments.iterator() {
                    passed_arguments.push(Value(impl: ValueImpl::JaktString(argument), span: call_span))
                }
                yield [Value(
                    impl: ValueImpl::JaktArray(values: passed_arguments, type_id: first_main_param!.variable.type_id)
                    span: call_span
                )]
            }
            else => []
        }

        let main_result = interpreter.execute(
            function_to_run_id: main_function_id!
            namespace_
            this_argument: None
            arguments
            call_span)

        match main_result {
            Return(x) => match x.impl {
                // FIXME: I64 should not be accepted here, we're just not performing implicit type conversions in the interpreter.
                CInt(ret_val) | I64(ret_val) => {
                    return ret_val
                }
                Void => {
                    return 0
                }
                else => {
                    eprintln("Error: Main function must return an integer")
                    return 1
                }
            }

            Throw(x) => {
                eprintln("Error: Main function threw: {}", serialize_ast_node(value_to_checked_expression(x, interpreter)))
                return 1
            }
        }
    }

    if goto_def.has_value() {
        let index = goto_def!.to_uint()! as! usize;

        let result = ide::find_definition_in_program(program: checked_program, span: Span(file_id: FileId(id: 1), start: index, end: index))

        if result.file_id.id == 1 {
            println("{{\"start\": {}, \"end\": {}}}", result.start, result.end);
        } else {
            let file_path = compiler.get_file_path(result.file_id)
            
            println("{{\"start\": {}, \"end\": {}, \"file\": \"{}\"}}", result.start, result.end, escape_for_quotes(file_path!.path));
        }
        return 0
    }
    if goto_type_def.has_value() {
        let index = goto_type_def!.to_uint()! as! usize;

        let result = ide::find_type_definition_in_program(program: checked_program, span: Span(file_id: FileId(id: 1), start: index, end: index))

        if result.file_id.id == 1 {
            println("{{\"start\": {}, \"end\": {}}}", result.start, result.end);
        } else {
            let file_path = compiler.get_file_path(result.file_id)

            println("{{\"start\": {}, \"end\": {}, \"file\": \"{}\"}}", result.start, result.end, escape_for_quotes(file_path!.path));
        }
        return 0
    }
    if hover.has_value() {
        let index = hover!.to_uint()! as! usize;
        
        let result = ide::find_typename_in_program(program: checked_program, span: Span(file_id: FileId(id: 1), start: index, end: index))

        if result.has_value() {
            println("{{\"hover\": \"{}\"}}", result!)
        }
        return 0
    }
    if completions.has_value() {
        let index = completions!.to_uint()! as! usize;

        let result = ide::find_dot_completions(program: checked_program, span: Span(file_id: FileId(id: 1), start: index, end: index))

        print("{{\"completions\": [");
        mut first = true
        for completion in result.iterator() {
            if not first {
                print(", ")
            } else {
                first = false
            }
            print("\"{}\"", completion)
        }
        println("]}}");
        return 0
    }

    if typechecker_debug {
        println("{:#}", checked_program);
    }

    compiler.print_errors()

    if not compiler.errors.is_empty() {
        return 1
    }

    if check_only {
        return 0
    }

    let output = CodeGenerator::generate(compiler, checked_program, debug_info: codegen_debug)

    mut cpp_filename: String = ""
    mut output_filename: String = ""

    if (write_source_to_file or build_executable or run_executable) {
        
        if set_output_filename.has_value() {
            cpp_filename = binary_dir + "/" + set_output_filename! + ".cpp"
            output_filename = binary_dir + "/" + set_output_filename!
        } else {
            let basename_without_extension = file_path.basename().substring(start: 0, length: file_path.basename().length() - 5)
            cpp_filename = binary_dir + "/" + basename_without_extension + ".cpp"
            output_filename = binary_dir + "/" + basename_without_extension
        }

        try {
            write_to_file(data: output, output_filename: cpp_filename)
        } catch error {
            eprintln("Could not write file: {} ({})", cpp_filename, error);
            return error.code();
        }

        if prettify_cpp_source {
            mut command = clang_format_path + " -i " + cpp_filename
            if dot_clang_format_path.has_value() {
                command+= " --style=file:" + dot_clang_format_path!
            }
            system(command.c_string())
        }
    } else {
        println("{}", output)
    }

    if (build_executable or run_executable) {
        let compiler_status = run_compiler(
            cxx_compiler_path
            cpp_filename
            output_filename
            runtime_path
            extra_include_paths
            extra_lib_paths
            extra_link_libs
            optimize
        )

        if run_executable and compiler_status == 0 {
            return system(output_filename.c_string())
        } else {
            if compiler_status == 0 {
                return 0
            } else {
                return 1
            }
        }
    }
}

function run_compiler(cxx_compiler_path: String, cpp_filename: String, output_filename: String, runtime_path: String, extra_include_paths: [String], extra_lib_paths: [String], extra_link_libs: [String], optimize: bool) throws -> c_int {
    mut file_path = FilePath(path: cxx_compiler_path)

    mut extra_flags: [String] = []
    if file_path.basename() == "g++" {
        extra_flags.push("-Wno-literal-suffix")
        extra_flags.push("-Wno-unused-parameter")
        extra_flags.push("-Wno-unused-but-set-variable")
        extra_flags.push("-Wno-unused-result")
        extra_flags.push("-Wno-implicit-fallthrough")
        extra_flags.push("-Wno-unused-command-line-argument")
    }

    mut compile_args = [
        cxx_compiler_path
        "-fdiagnostics-color=always"
        "-std=c++20"
        "-fno-exceptions"
        "-Wno-unknown-warning-option"
        "-Wno-trigraphs"
        "-Wno-parentheses-equality"
        "-Wno-unqualified-std-cast-call"
        "-Wno-user-defined-literals"
        "-Wno-deprecated-declarations"  
    ]

    if optimize {
        compile_args.push("-O3")
    }

    if not extra_flags.is_empty() {
        for flag in extra_flags.iterator() {
            compile_args.push(flag)
        }
    }

    compile_args.push("-I")
    compile_args.push(runtime_path)

    compile_args.push("-o")
    compile_args.push(output_filename)

    compile_args.push(cpp_filename)
    if not extra_include_paths.is_empty() {
        compile_args.add_capacity(extra_include_paths.size() * 2)
        for path in extra_include_paths.iterator() {
            compile_args.push("-I")
            compile_args.push(path)
        }
    }
    if not extra_lib_paths.is_empty() {
        compile_args.add_capacity(extra_lib_paths.size() * 2)
        for path in extra_lib_paths.iterator() {
            compile_args.push("-L")
            compile_args.push(path)
        }
    }
    if not extra_link_libs.is_empty() {
        compile_args.add_capacity(extra_link_libs.size())
        for path in extra_link_libs.iterator() {
            compile_args.push("-l" + path)
        }
    }
    mut command = ""
    for compile_arg in compile_args.iterator() {
        command += compile_arg
        command += " "
    }
    return system(command.c_string())
}

function write_to_file(data: String, output_filename: String) throws {
    mut outfile = File::open_for_writing(output_filename)
    mut bytes: [u8] = []
    for i in 0..data.length() {
        bytes.push(data.byte_at(i))
    }
    outfile.write(bytes)
}
