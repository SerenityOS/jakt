/// Expect:
/// - output: ""

// Copyright (c) 2022, JT <jt@serenityos.org>
// Copyright (c) 2022, Andreas Kling <kling@serenityos.org>
//
// SPDX-License-Identifier: BSD-2-Clause

import codegen { CodeGenerator }
import error { JaktError, print_error }
import lexer { Lexer }
import parser { Parser }
import typechecker { Typechecker }

function usage() => "usage: jakt [-h] [OPTIONS] <path>"
function help() => "Flags:\n  -h\t\tPrint this help and exit.\n  -l\t\tPrint debug info for the lexer.\n  -p\t\tPrint debug info for the parser."

function flag(args: [String], anon name: String) => args.contains("-" + name)

function main(args: [String]) {
    if args.size() <= 1 {
        eprintln("{}", usage())
        return 1
    }
    
    if flag(args, "h") {
        println("{}\n", usage())
        println("{}", help())
        return 0
    }

    let lexer_debug = flag(args, "l")
    let parser_debug = flag(args, "p")
    let build_executable = flag(args, "b")

    mut file_name: String? = None
    mut first_arg = true

    for arg in args.iterator() {
        if first_arg {
            first_arg = false
            continue
        }
        if arg != "-h" and arg != "-l" and arg != "-p" and arg != "-b" {
            if file_name.has_value() {
                eprintln("you can only pass one file")
                eprintln("{}", usage())
                return 1
            } else {
                file_name = arg
            }
        }
    }
    if not file_name.has_value() {
        eprintln("you must pass a source file")
        eprintln("{}", usage())
        return 1
    }

    mut file = File::open_for_reading(file_name!)
    let file_contents = file.read_all()

    mut errors: [JaktError] = []

    let tokens = Lexer::lex(input: file_contents, errors)

    if lexer_debug {
        for token in tokens.iterator() {
            println("token: {}", token)
        }
    }

    let parsed_namespace = Parser::parse(tokens, errors)

    if parser_debug {
        println("{:#}", parsed_namespace);
    }

    let checked_program = Typechecker::typecheck(parsed_namespace, errors)

    for error in errors.iterator() {
        print_error(file_name: file_name!, file_contents, error)
    }

    if not errors.is_empty() {
        return 1
    }

    let output = CodeGenerator::generate(checked_program)

    if build_executable {
        let output_filename = "output.cpp"
        write_to_file(data: output, output_filename)
        run_compiler(output_filename)
    } else {
        println("{}", output)
    }
}

function run_compiler(output_filename: String) throws {
    mut compile_args = [
        "clang++"
        "-fcolor-diagnostics"
        "-std=c++20"
        "-Wno-unknown-warning-option"
        "-Wno-trigraphs"
        "-Wno-parentheses-equality"
        "-Wno-unqualified-std-cast-call"
        "-Wno-user-defined-literals"
        "-Wno-deprecated-declarations"
        "-Iruntime"
    ]
    compile_args.push(output_filename)
    mut command = ""
    for compile_arg in compile_args.iterator() {
        command += compile_arg
        command += " "
    }
    system(command.c_string())
}

function write_to_file(data: String, output_filename: String) throws {
    mut outfile = File::open_for_writing(output_filename)
    mut bytes: [u8] = []
    for i in 0..data.length() {
        bytes.push(data.byte_at(i))
    }
    outfile.write(bytes)
}
