// Copyright (c) 2022, JT <jt@serenityos.org>
// Copyright (c) 2022, Andreas Kling <kling@serenityos.org>
//
// SPDX-License-Identifier: BSD-2-Clause

import utility { Span }

enum JaktError {
    Message(message: String, span: Span)
    MessageWithHint(message: String, span: Span, hint: String, hint_span: Span)

    fn span(this) -> Span => match this {
        Message(span) => span
        MessageWithHint(span) => span
    }
}


fn print_error_json(file_name: String, error: JaktError) {
    match error {
        Message(message, span) => {
            display_message_with_span_json(MessageSeverity::Error, file_name, message, span)
        }
        MessageWithHint(message, span, hint, hint_span) => {
            display_message_with_span_json(MessageSeverity::Error, file_name, message, span)
            display_message_with_span_json(MessageSeverity::Hint, file_name, message: hint, span: hint_span)
        }
    }
}

fn print_error(file_name: String, file_contents: [u8]?, error: JaktError) throws {
    match error {
        Message(message, span) => {
            display_message_with_span(MessageSeverity::Error, file_name, contents: file_contents, message, span)
        }
        MessageWithHint(message, span, hint, hint_span) => {
            display_message_with_span(MessageSeverity::Error, file_name, contents: file_contents, message, span)
            display_message_with_span(MessageSeverity::Hint, file_name, contents: file_contents, message: hint, span: hint_span)
        }
    }
}

enum MessageSeverity {
    Hint
    Error
    public fn name(this) -> String => match this {
        Hint => "Hint"
        Error => "Error"
    }
    public fn ansi_color_code(this) -> String => match this {
        Hint => "94"  // Bright Blue
        Error => "31" // Red
    }
}


fn display_message_with_span_json(anon severity: MessageSeverity, file_name: String, message: String, span: Span)
{
    println("{{\"type\":\"diagnostic\",\"message\":\"{}\",\"severity\":\"{}\",\"file_id\":{},\"span\":{{\"start\":{},\"end\":{}}}}}"
        message, severity.name(), span.file_id.id, span.start, span.end)
}

fn display_message_with_span(anon severity: MessageSeverity, file_name: String, contents: [u8]?, message: String, span: Span) {
    eprintln("{}: {}", severity.name(), message)

    if not contents.has_value() {
        return
    }

    let file_contents = contents!
    let line_spans = gather_line_spans(file_contents)

    mut line_index = 0uz
    mut error_start_index = 0uz
    mut largest_line_number = 0uz
    // Determine largest (human readable) line number and the index of the first error line
    while line_index < line_spans.size() {
        if span.start >= line_spans[line_index].0 and span.start <= line_spans[line_index].1 {
            error_start_index = line_index
        }

        if span.end >= line_spans[line_index].0 and span.end <= line_spans[line_index].1 {
            largest_line_number = line_index + 2 // 1 (row number) + 1 (extra source line)
        }

        ++line_index
    }

    let width = format("{}", largest_line_number).length()

    // Start printing the error and additional info, including source line numbers surrounded with a nice border

    line_index = error_start_index
    let column_index = span.start - line_spans[line_index].0

    // Print top of the border, file name, line number and column of the error
    for x in 0..(width + 2) {
        eprint("─")
    }
    eprintln("┬─ \u001b[33m{}:{}:{}\u001b[0m", file_name, line_index + 1, column_index + 1)

    // Print one line before the error for the context
    if line_index > 0 {
        print_source_line(severity, file_contents, file_span: line_spans[line_index - 1], error_span: span, line_number: line_index, largest_line_number)
    }

    // Print the lines that include the error
    while line_index < line_spans.size() and span.end > line_spans[line_index].0 {
        print_source_line(severity, file_contents, file_span: line_spans[line_index], error_span: span, line_number: line_index + 1, largest_line_number)
        if span.end <= line_spans[line_index].1 {
            print_underline(severity, width, file_span: line_spans[line_index], error_span: span, line_number: line_index + 1, largest_line_number)
            break
        }
        ++line_index
    }

    // Print the error label
    for x in 0..(width + 2) {
        eprint(" ")
    }
    eprint("│")
    for x in 0..(span.end - line_spans[line_index].0) {
        eprint(" ")
    }
    eprintln("\u001b[{}m╰─ {}\u001b[0m", severity.ansi_color_code(), message)

    ++line_index

    // Print one line after the error for context
    if line_index < line_spans.size() {
        print_source_line(severity, file_contents, file_span: line_spans[line_index], error_span: span, line_number: line_index + 1, largest_line_number)
    }

    // Print bottom of the border
    eprint("\u001b[0m")
    for x in 0..(width + 2) {
        eprint("─")
    }
    eprintln("┴─")
}

fn print_source_line(severity: MessageSeverity, file_contents: [u8], file_span: (usize, usize), error_span: Span, line_number: usize, largest_line_number: usize) {
    mut index = file_span.0

    let largest_width = format("{}", largest_line_number).length()
    let current_width = format("{}", line_number).length()

    eprint(" {}", line_number)
    for _ in 0..(largest_width - current_width) {
        eprint(" ")
    }
    eprint(" │ ")

    while index <= file_span.1 {
        mut builder = StringBuilder::create()

        loop {
            mut c = b' '
            if index < file_span.1 {
                c = file_contents[index]
            } else if error_span.start == error_span.end and index == error_span.start {
                c = b'_'
            }

            builder.append(c)

            if c > 127 {
                index += 1
                continue
            }
            break
        }

        if (index == error_span.start) {
            eprint("\u001b[{}m", severity.ansi_color_code())
        }
        if (index == error_span.end) {
            eprint("\u001b[0m")
        }

        eprint("{}", builder.to_string())

        ++index
    }
    eprintln("")
}

fn print_underline(severity: MessageSeverity, width: usize, file_span: (usize, usize), error_span: Span, line_number: usize, largest_line_number: usize) {
    // Print the error label
    for x in 0..(width + 2) {
        eprint(" ")
    }
    eprint("│ ")

    for index in (file_span.0)..(file_span.1) {
        if index == error_span.start {
            eprint("\u001b[{}m", severity.ansi_color_code())
        }

        if (index == error_span.end - 1) {
            eprint("┬\u001b[0m")
        }
        else if (index >= error_span.start) and (index < error_span.end) {
            eprint("─")
        }
        else {
            eprint(" ")
        }
    }
    eprintln("")
}

fn gather_line_spans(file_contents: [u8]) -> [(usize, usize)] {
    mut idx = 0uz
    mut output: [(usize, usize)] = []

    mut start = idx
    while idx < file_contents.size() {
        if file_contents[idx] == b'\n' {
            output.push((start, idx))
            start = idx + 1
        }
        idx += 1
    }
    if start <= idx {
        output.push((start, idx))
    }

    return output
}
