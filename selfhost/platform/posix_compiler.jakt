import jakt::path { Path }

fn run_compiler(
    cxx_compiler_path: String
    cpp_filename: String
    output_filename: String
    runtime_path: String
    extra_include_paths: [String]
    extra_lib_paths: [String]
    extra_link_libs: [String]
    optimize: bool
    extra_compiler_flags: [String]
    use_ccache: bool
) throws -> [String] {
    mut file_path = Path::from_string(cxx_compiler_path)

    mut extra_flags: [String] = []
    if file_path.basename().contains("g++") {
        extra_flags.push("-Wno-literal-suffix")
        extra_flags.push("-Wno-unused-parameter")
        extra_flags.push("-Wno-unused-but-set-variable")
        extra_flags.push("-Wno-unused-result")
        extra_flags.push("-Wno-implicit-fallthrough")
        extra_flags.push("-Wno-unused-command-line-argument")
    }
    extra_flags.push_values(&extra_compiler_flags)

    mut compile_args = [
        cxx_compiler_path
        "-fdiagnostics-color=always"
        "-std=c++20"
        "-fno-exceptions"
        "-Wno-unknown-warning-option"
        "-Wno-trigraphs"
        "-Wno-parentheses-equality"
        "-Wno-unqualified-std-cast-call"
        "-Wno-user-defined-literals"
        "-Wno-deprecated-declarations"
        "-Wno-invalid-offsetof"
        "-g"
    ]

    if use_ccache {
        compile_args.insert(before_index: 0, value: "ccache")
    }

    if optimize {
        compile_args.push("-O3")
    }

    if not extra_flags.is_empty() {
        compile_args.push_values(&extra_flags)
    }

    compile_args.push("-I")
    compile_args.push(runtime_path)

    compile_args.push("-o")
    compile_args.push(output_filename)

    compile_args.push(cpp_filename)
    if not extra_include_paths.is_empty() {
        compile_args.add_capacity(extra_include_paths.size() * 2)
        for path in extra_include_paths {
            compile_args.push("-I")
            compile_args.push(path)
        }
    }
    if not extra_lib_paths.is_empty() {
        compile_args.add_capacity(extra_lib_paths.size() * 2)
        for path in extra_lib_paths {
            compile_args.push("-L")
            compile_args.push(path)
        }
    }
    if not extra_link_libs.is_empty() {
        compile_args.add_capacity(extra_link_libs.size())
        for path in extra_link_libs {
            compile_args.push("-l" + path)
        }
    }

    return compile_args
}
