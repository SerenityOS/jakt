import typechecker {
    BuiltinType, CheckedExpression, CheckedProgram, CheckedUnaryOperator, GenericInferences, ImportList, ImportName
    Interpreter, InterpreterScope, LoadedModule, ModuleId, SafetyMode, ScopeId, TypeId, Typechecker, builtin
}
import parser {
    ParsedModuleImport
}
import compiler { Compiler, FileId }
import lexer { Lexer, Token }
import parser { Parser }
import utility { Span, allocate, null }
import error { JaktError }
import interpreter { value_to_checked_expression }
import jakt::path { Path }

import platform { repl_backend }
import repl_backend() { Editor, LineResult, Style, Color, XTermColor }

fn serialize_unary_operation(anon op: CheckedUnaryOperator, anon expr: String) throws -> String => match op {
    PreIncrement => "++" + expr
    PostIncrement => expr + "++"
    PreDecrement => "--" + expr
    PostDecrement => expr + "--"
    Negate => "-" + expr
    Dereference => "*" + expr
    RawAddress => "&raw " + expr
    Reference => "&" + expr
    MutableReference => "&mut " + expr
    LogicalNot => "not " + expr
    BitwiseNot => "~" + expr
    else => "(<Unimplemented unary operator> " + expr + ")"
}

fn serialize_ast_node(anon node: CheckedExpression) throws -> String => match node {
    Boolean(val) => format("{}", val)
    NumericConstant(val) => match val {
        I8(value) => format("{}i8", value)
        I16(value) => format("{}i16", value)
        I32(value) => format("{}i32", value)
        I64(value) => format("{}i64", value)
        U8(value) => format("{}u8", value)
        U16(value) => format("{}u16", value)
        U32(value) => format("{}u32", value)
        U64(value) => format("{}u64", value)
        F32(value) => format("{}f32", value)
        F64(value) => format("{}f64", value)
        USize(value) => format("{}uz", value)
    }
    QuotedString(val) => match val.type_id.equals(builtin(BuiltinType::JaktString)) {
        true => format("\"{}\"", val.to_string())
        else => format("(overloaded) \"{}\"", val.to_string())
    }
    ByteConstant(val) => format("b'{}'", val)
    CharacterConstant(val) => format("'{}'", val)
    CCharacterConstant(val) => format("c'{}'", val)
    UnaryOp(expr, op) => serialize_unary_operation(op, serialize_ast_node(expr))
    JaktTuple(vals) => {
        mut builder = StringBuilder::create()
        builder.append("(")
        for i in 0..vals.size() {
            if i != 0 {
                builder.append(", ")
            }
            builder.append(serialize_ast_node(vals[i]))
        }
        builder.append(")")
        yield builder.to_string()
    }
    Range(from, to) => {
        mut from_output = ""
        if from.has_value() {
            from_output = serialize_ast_node(from!)
        }

        mut to_output = ""
        if to.has_value() {
            to_output = serialize_ast_node(to!)
        }

        yield format("{}..{}", from_output, to_output)
    }
    JaktArray(vals) => {
        mut builder = StringBuilder::create()
        builder.append("[")
        for i in 0..vals.size() {
            if i != 0 {
                builder.append(", ")
            }
            builder.append(serialize_ast_node(vals[i]))
        }
        builder.append("]")
        yield builder.to_string()
    }
    JaktSet(vals) => {
        mut builder = StringBuilder::create()
        builder.append("{")
        for i in 0..vals.size() {
            if i != 0 {
                builder.append(", ")
            }
            builder.append(serialize_ast_node(vals[i]))
        }
        builder.append("}")
        yield builder.to_string()
    }
    JaktDictionary(vals) => {
        mut builder = StringBuilder::create()
        builder.append("[")
        for i in 0..vals.size() {
            if i != 0 {
                builder.append(", ")
            }
            let val = vals[i]
            builder.append(serialize_ast_node(val.0))
            builder.append(": ")
            builder.append(serialize_ast_node(val.1))
        }
        builder.append("]")
        yield builder.to_string()
    }
    IndexedExpression(expr, index) => format("{}[{}]", serialize_ast_node(expr), serialize_ast_node(index))
    IndexedDictionary(expr, index) => format("{}[{}]", serialize_ast_node(expr), serialize_ast_node(index))
    IndexedTuple(expr, index)
    | IndexedStruct(expr, index) => format("{}.{}", serialize_ast_node(expr), index)
    NamespacedVar(namespaces, var) => {
        mut builder = StringBuilder::create()
        for namespace_ in namespaces {
            builder.append(namespace_.name)
            builder.append("::")
        }
        builder.append(var.name)
        yield builder.to_string()
    }
    Var(var) => var.name
    OptionalNone => "None"
    OptionalSome(expr) => format("Some({})", serialize_ast_node(expr))
    ForcedUnwrap(expr) => format("{}!", serialize_ast_node(expr))
    Call(call) => {
        mut builder = StringBuilder::create()

        for namespace_ in call.namespace_ {
            builder.append(namespace_.name)
            builder.append("::")
        }
        builder.append(call.name)
        builder.append("(")
        for i in 0..call.args.size() {
            if i != 0 {
                builder.append(", ")
            }
            let arg = call.args[i]
            if not arg.0.is_empty() {
                builder.append(arg.0)
                builder.append(": ")
            }
            builder.append(serialize_ast_node(arg.1))
        }
        builder.append(")")
        yield builder.to_string()
    }
    Garbage => "<Garbage>"
    else => "<Unimplemented>"
}

struct REPL {
    compiler: Compiler
    typechecker: Typechecker
    root_scope_id: ScopeId
    root_interpreter_scope: InterpreterScope
    file_id: FileId

    fn create(runtime_path: Path, target_triple: String? = None, user_configuration: [String:String]) throws -> REPL {
        mut compiler = Compiler(
            files: []
            file_ids: [:]
            errors: []
            current_file: None
            current_file_contents: []
            dump_lexer: false
            dump_parser: false
            ignore_parser_errors: false
            debug_print: false
            std_include_path: runtime_path
            include_paths: []
            json_errors: false
            dump_type_hints: false
            dump_try_hints: false
            optimize: false
            target_triple
            user_configuration
            binary_dir: Path::from_string("build")
            assume_main_file_path: Path::from_string("repl.jakt")
        )

        compiler.load_prelude()
        let file_id = compiler.get_file_id_or_register(file: Path::from_string("<repl>"))

        let placeholder_module_id = ModuleId(id: 0)
        let root_module_name = "repl"

        mut typechecker = Typechecker(
            compiler
            program: CheckedProgram(compiler),
            current_module_id: placeholder_module_id,
            current_struct_type_id: TypeId::none()
            current_function_id: None
            inside_defer: false
            checkidx: 0uz
            ignore_errors: false
            dump_type_hints: compiler.dump_type_hints
            dump_try_hints: compiler.dump_try_hints
            lambda_count: 0
            generic_inferences: GenericInferences(values: [:])
            root_module_name
            cpp_import_cache: [:]
        )

        compiler.current_file = file_id
        typechecker.include_prelude()

        let root_module_id = typechecker.create_module(name: root_module_name, is_root: true)
        typechecker.current_module_id = root_module_id
        typechecker.program.set_loaded_module(
            module_name: root_module_name
            loaded_module: LoadedModule(
                module_id: root_module_id
                file_id: file_id
            )
        )

        let PRELUDE_SCOPE_ID: ScopeId = typechecker.prelude_scope_id()
        let root_scope_id = typechecker.create_scope(parent_scope_id: PRELUDE_SCOPE_ID, can_throw: true,  debug_name: "root")
        typechecker.typecheck_module_import(
            import_: ParsedModuleImport(
                module_name: ImportName::Literal(
                    name: "jakt::prelude::prelude"
                    span: Span(file_id: FileId(id: 0), start: 0, end: 0)
                ),
                alias_name: None
                import_list: ImportList::All
                relative_path: false
                parent_path_count: 0
            )
            scope_id: root_scope_id
        )

        let root_interpreter_scope = InterpreterScope::create()

        return REPL(
            compiler
            typechecker
            root_scope_id
            root_interpreter_scope
            file_id
        )
    }

    fn handle_possible_error(mut this) throws -> bool {
        .compiler.print_errors()
        let has_error = .compiler.errors.size() > 0
        let arr: [JaktError] = []
        .compiler.errors = arr
        return has_error
    }

    fn line_to_bytes(anon line: String) -> [u8] {
      mut pos: usize   = 0
      mut bytes_: [u8] = []

      bytes_.ensure_capacity(line.length())

      while pos < line.length() {
          bytes_.push(line.byte_at(pos))
          ++pos
      }

      return bytes_
    }

    fn check_parens(tokens: [Token]) -> bool {
        mut unmatched_parens   = 0
        mut unmatched_brackets = 0
        mut unmatched_curlies  = 0

        for token in tokens {
            match token {
                LParen  => { unmatched_parens   += 1 }
                RParen  => { unmatched_parens   -= 1 }
                LSquare => { unmatched_brackets += 1 }
                RSquare => { unmatched_brackets -= 1 }
                LCurly  => { unmatched_curlies  += 1 }
                RCurly  => { unmatched_curlies  -= 1 }
                else    => {}
            }
        }

        return unmatched_parens == 0 and unmatched_brackets == 0 and unmatched_curlies == 0
    }

    fn run(mut this) throws {
        let syntax_highlight_handler = fn[this](anon editor: &mut Editor) throws -> void {
            let line = editor.get_active_buffer()
            mut pos: usize = 0
            mut bytes_: [u8] = []
            bytes_.ensure_capacity(line.length())
            while pos < line.length() {
                bytes_.push(line.byte_at(pos))
                ++pos
            }
            .compiler.current_file = .file_id
            .compiler.current_file_contents = bytes_
            defer {
                let arr: [JaktError] = []
                .compiler.errors = arr
            }

            for token in try Lexer::lex(compiler: .compiler) catch { return } {
                match token {
                    SingleQuotedString(span)
                    | QuotedString(span)
                    => editor.highlight(span, Style(foreground: Color::XTerm(XTermColor::Green)))
                    Number(span) => editor.highlight(span, Style(foreground: Color::XTerm(XTermColor::Blue)))
                    And(span)
                    | Anon(span)
                    | As(span)
                    | Boxed(span)
                    | Break(span)
                    | Catch(span)
                    | Class(span)
                    | Continue(span)
                    | Cpp(span)
                    | Defer(span)
                    | Destructor(span)
                    | Else(span)
                    | Enum(span)
                    | Extern(span)
                    | False(span)
                    | For(span)
                    | Fn(span)
                    | Comptime(span)
                    | If(span)
                    | Import(span)
                    | In(span)
                    | Is(span)
                    | Let(span)
                    | Loop(span)
                    | Match(span)
                    | Mut(span)
                    | Namespace(span)
                    | Not(span)
                    | Or(span)
                    | Override(span)
                    | Private(span)
                    | Public(span)
                    | Raw(span)
                    | Reflect(span)
                    | Return(span)
                    | Restricted(span)
                    | Sizeof(span)
                    | Struct(span)
                    | This(span) | Dot(span)
                    | Throw(span)
                    | Throws(span)
                    | True(span)
                    | Try(span)
                    | Unsafe(span)
                    | Virtual(span)
                    | Weak(span)
                    | While(span)
                    | Yield(span)
                    | Guard(span)
                    | Implements(span)
                    | Requires(span)
                    | Trait(span)
                    => editor.highlight(span, Style(foreground: Color::XTerm(XTermColor::Yellow)))
                    DotDot(span) => editor.highlight(span, Style(foreground: Color::XTerm(XTermColor::Cyan)))
                    Eol(span, comment) => {
                        if comment.has_value() { editor.highlight(span, Style(foreground: Color::Components(red: 128, green: 128, blue: 128))) }
                    }
                    else => {}
                }
            }
        }

        mut editor = Editor::create(
            prompt: "> "
            &syntax_highlight_handler
        )
        defer editor.destroy()

        loop {
            if not .compiler.errors.is_empty() {
                .compiler.print_errors()
                let arr: [JaktError] = []
                .compiler.errors = arr
            }

            let line_result = try editor.get_line() catch error {
                return
            }

            guard line_result is Line(line) else {
                break
            }

            if line == "\n" {
              continue
            }

            if line == ".exit\n" {
                break
            }

            .compiler.current_file = .file_id
            .compiler.current_file_contents = line_to_bytes(line)

            mut tokens = try Lexer::lex(compiler: .compiler) catch {
                continue
            }

            if tokens.is_empty() {
                continue
            }

            if not check_parens(tokens) {
                mut builder = StringBuilder::create()
                builder.append(line)

                while not check_parens(tokens) {
                    let line_result = try editor.get_line(prompt: "..") catch error { return }

                    guard line_result is Line(line) else {
                        return
                    }

                    builder.append(line)

                    .compiler.current_file = .file_id
                    .compiler.current_file_contents = line_to_bytes(builder.to_string())

                    tokens = try Lexer::lex(compiler: .compiler) catch { continue }
                }
            }

            mut parser = Parser(index: 0, tokens, compiler: .compiler)

            let first_token = tokens.first()!
            if first_token is Fn
                or first_token is Comptime
                or first_token is Struct
                or first_token is Class
                or first_token is Enum
                or first_token is Boxed
                or first_token is Namespace
                or first_token is Import
                or first_token is Trait {

                let parsed_namespace = try parser.parse_namespace() catch {
                    .handle_possible_error()
                    continue
                }

                if .handle_possible_error() {
                    continue
                }

                try .typechecker.typecheck_module(parsed_namespace, scope_id: .root_scope_id) catch {
                    .handle_possible_error()
                    continue
                }

                .handle_possible_error()

                continue
            }

            let parsed_statement = try parser.parse_statement(inside_block: true) catch {
                .handle_possible_error()
                continue
            }

            if .handle_possible_error() {
                continue
            }

            let checked_statement = try .typechecker.typecheck_statement(statement: parsed_statement, scope_id: .root_scope_id, safety_mode: SafetyMode::Safe) catch {
                .handle_possible_error()
                continue
            }

            if .handle_possible_error() {
                continue
            }

            mut interpreter = .typechecker.interpreter()

            let result = try interpreter.execute_statement(
                statement: checked_statement
                scope: .root_interpreter_scope
                call_span: Span(
                    file_id: .file_id
                    start: 0
                    end: line.length()
                )
            ) catch {
                .handle_possible_error()
                continue
            }

            if .handle_possible_error() {
                continue
            }

            match result {
                Return(value) | JustValue(value) | Throw(value) => {
                    if value.impl is Void {
                        continue
                    }
                }
                Break | Continue | Yield => {}
            }

            let output = match result {
                Return(value) | JustValue(value) => serialize_ast_node(value_to_checked_expression(value, interpreter)),
                Throw(value) => "throw " + serialize_ast_node(value_to_checked_expression(value, interpreter))
                Break | Continue | Yield => {
                    continue
                }
            }
            println("= {}", output)
        }
    }
}
