// Copyright (c) 2022, JT <jt@serenityos.org>
// Copyright (c) 2022, Andreas Kling <kling@serenityos.org>
// Copyright (c) 2022, Kyle Lanmon <kyle.lanmon@gmail.com>
// Copyright (c) 2022, Adler Oliveira <adler.rs.oliveira@gmail.com>
//
// SPDX-License-Identifier: BSD-2-Clause

import parser { BinaryOperator, FunctionLinkage, ParsedBlock }
import typechecker {
    BuiltinType, CheckedBinaryOperator, CheckedBlock, CheckedCall, CheckedEnum, CheckedExpression, CheckedFunction
    CheckedMatchBody, CheckedMatchCase, CheckedProgram, CheckedStatement, CheckedStruct, CheckedVariable
    CheckedVisibility, EnumId, FunctionId, Module, ModuleId, Scope, ScopeId, StructId, Type, TypeId, VarId, builtin
    never_type_id, unknown_type_id, void_type_id
}
import types { CheckedGenericParameter, CheckedParameter, FunctionGenericParameter, SafetyMode, StructLikeId }
import utility { Queue, Span, interpret_escapes, join, panic, prepend_to_each, todo }
import compiler { Compiler }
import interpreter { Interpreter, TypecheckFunctions, value_to_checked_expression }

enum AllowedControlExits {
    /// No control exit statements allowed
    Nothing
    /// Only `return` allowed
    JustReturn
    /// `continue`/`break` allowed: `return` is also allowed in this context
    AtLoop

    fn allow_return(this) -> AllowedControlExits => match this {
        Nothing | JustReturn => AllowedControlExits::JustReturn
        AtLoop => AllowedControlExits::AtLoop
    }
}

fn are_loop_exits_allowed(anon allowed_control_exits: AllowedControlExits) -> bool => allowed_control_exits is AtLoop
fn is_return_allowed(anon allowed_control_exits: AllowedControlExits) -> bool => not allowed_control_exits is Nothing

struct ControlFlowState {
    allowed_exits: AllowedControlExits
    /// Whether `break` and `continue` should use `return ExplicitValueOrControlFlow` instead of
    /// C++'s `break`/`continue`.
    passes_through_match: bool
    passes_through_try: bool
    match_nest_level: usize

    fn no_control_flow() -> ControlFlowState {
        return ControlFlowState(
            allowed_exits: AllowedControlExits::Nothing
            passes_through_match: false
            passes_through_try: false
            match_nest_level: 0
        )
    }
    fn enter_function(this) -> ControlFlowState {
        return ControlFlowState(
            allowed_exits: AllowedControlExits::JustReturn
            passes_through_match: false
            passes_through_try: false
            match_nest_level: .match_nest_level
        )
    }
    fn enter_loop(this) -> ControlFlowState {
        return ControlFlowState(
            allowed_exits: AllowedControlExits::AtLoop
            passes_through_match: false
            passes_through_try: .passes_through_try
            match_nest_level: 0
        )
    }
    fn enter_match(this) -> ControlFlowState {
        mut level = .match_nest_level
        if .passes_through_match {
            level = .match_nest_level + 1
        }
        return ControlFlowState(
            allowed_exits: .allowed_exits.allow_return()
            passes_through_match: true
            passes_through_try: .passes_through_try
            match_nest_level: level
        )
    }
    fn is_match_nested(this) -> bool => .match_nest_level != 0

    // Instead of trying to use `release_return()` which returns `void`, we'll
    // directly construct a `ExplicitValueOrControlFlow<MatchResult, void>` so
    // that the compiler doesn't try to construct one from a `void` value,
    // which it doesn't know how to do.
    fn nested_release_return_expr(func_return_type: TypeId, func_can_throw: bool, cpp_match_result_type: String) -> String {

        let returns_void = func_return_type == void_type_id() or func_return_type == unknown_type_id() or func_return_type == never_type_id()

        // The C++ function won't return 'void' if it can throw, but rather
        // `ErrorOr`.
        if returns_void and not func_can_throw {
            return format("JaktInternal::ExplicitValueOrControlFlow<{}, void>()", cpp_match_result_type)
        } else {
            return "_jakt_value.release_return()"
        }
    }

    fn apply_control_flow_macro(this, anon x: String, func_return_type: TypeId, func_can_throw: bool, cpp_match_result_type: String) throws -> String {
        if are_loop_exits_allowed(.allowed_exits) {
            if .is_match_nested() {

                return format(
                    "({{
    auto&& _jakt_value = {};
    if (_jakt_value.is_return())
        return {};
    if (_jakt_value.is_loop_break())
        return JaktInternal::LoopBreak {{}};
    if (_jakt_value.is_loop_continue())
        return JaktInternal::LoopContinue {{}};
    _jakt_value.release_value();
}})",
                    x,
                    nested_release_return_expr(func_return_type, func_can_throw, cpp_match_result_type)
                )
            }
            return format(
                "({{
    auto&& _jakt_value = {};
    if (_jakt_value.is_return())
        return _jakt_value.release_return();
    if (_jakt_value.is_loop_break())
        break;
    if (_jakt_value.is_loop_continue())
        continue;
    _jakt_value.release_value();
}})",
                x
            )
        }
        return format(
            "({{
    auto&& _jakt_value = {};
    if (_jakt_value.is_return())
        return {};
    _jakt_value.release_value();
}})",
            x,
            match .is_match_nested() {
                false => "_jakt_value.release_return()",
                true => nested_release_return_expr(func_return_type, func_can_throw, cpp_match_result_type),
            }
        )
    }
}

struct LineSpan {
    start: usize
    end: usize
}

struct CodegenDebugInfo {
    // FIXME: Add support for multiple source files
    compiler: Compiler
    line_spans: [usize: [LineSpan]]
    statement_span_comments: bool

    fn span_to_source_location(mut this, anon span: Span) throws -> String {
        if .line_spans.is_empty() {
            .gather_line_spans()
        }

        let file_idx = span.file_id.id

        if not .line_spans.contains(file_idx) {
            return ""
        }

        mut line_index = 0uz
        while line_index < .line_spans[file_idx].size() {
            if span.start >= .line_spans[file_idx][line_index].start and span.start <= .line_spans[file_idx][line_index].end {
                let column_index = span.start - .line_spans[file_idx][line_index].start
                return format("{} \"{}\"", line_index+1, .compiler.get_file_path(span.file_id)!.to_string())
            }
            line_index += 1
        }

        panic("Reached end of file and could not find index")
    }

    fn gather_line_spans(mut this) throws {
        for file in .compiler.file_ids {

            if file.0 == "__prelude__" {
                continue
            }

            .compiler.set_current_file(file.1)

            let file_idx = file.1.id

            // FIXME: remove when we can infer the type
            let empty_array: [LineSpan] = []
            .line_spans.set(file_idx, empty_array)

            mut idx = 0uz
            mut start = idx
            while idx < .compiler.current_file_contents.size() {
                if .compiler.current_file_contents[idx] == b'\n' {
                    .line_spans[file_idx].push(LineSpan(start, end: idx))
                    start = idx + 1
                }
                idx += 1
            }
            if start < idx {
                .line_spans[file_idx].push(LineSpan(start, end: idx))
            }
        }
    }
}

struct CodeGenerator {

    compiler: Compiler
    program: CheckedProgram

    control_flow_state: ControlFlowState
    entered_yieldable_blocks: [(String, String)] // label, variable name
    deferred_output: String
    current_function: CheckedFunction?
    inside_defer: bool
    debug_info: CodegenDebugInfo
    namespace_stack: [String]
    fresh_var_counter: usize
    fresh_label_counter: usize
    this_replacement: String? = None
    generic_inferences: [TypeId:TypeId]? = None

    // noreturn functions may not throw, so let them crash instead.
    fn current_error_handler(this) -> String {
        if .inside_defer or (
            .current_function.has_value() and
            .current_function!.return_type_id == never_type_id() and
            not .control_flow_state.passes_through_try
        ) {

            return "MUST"
        }

        return "TRY"
    }

    fn fresh_var(mut this) -> String => format("__jakt_var_{}", .fresh_var_counter++)

    fn fresh_label(mut this) -> String => format("__jakt_label_{}", .fresh_label_counter++)

    fn topologically_sort_modules(this) throws -> [ModuleId] {
        mut in_degrees: [usize:i64] = [:]

        for module in .program.modules {
            for imported_module in module.imports {
                let existing = in_degrees.get(imported_module.id) ?? 0
                in_degrees.set(imported_module.id, existing + 1)
            }

            if not in_degrees.contains(module.id.id) {
                in_degrees.set(module.id.id, 0)
            }
        }

        mut stack: [ModuleId] = []
        for module in .program.modules {
            if in_degrees[module.id.id] == 0 {
                stack.push(module.id)
            }
        }

        mut sorted_modules: [ModuleId] = []
        while not stack.is_empty() {
            let id = stack.pop()!
            sorted_modules.push(id)
            for imported_module in .program.modules[id.id].imports {
                let module_in_degrees = in_degrees[imported_module.id]
                in_degrees.set(imported_module.id, module_in_degrees - 1)
                if module_in_degrees == 1 {
                    stack.push(ModuleId(id: imported_module.id))
                }
            }
        }

        if sorted_modules.size() == .program.modules.size() {
            return sorted_modules
        }

        panic("Cyclic module imports")
    }

    fn generate(compiler: Compiler, anon program: CheckedProgram, debug_info: bool) throws -> [String:(String, String)] {
        mut generator = CodeGenerator(
            compiler
            program
            control_flow_state: ControlFlowState(
                allowed_exits: AllowedControlExits::Nothing
                passes_through_match: false
                passes_through_try: false
                match_nest_level: 0
            )
            entered_yieldable_blocks: []
            deferred_output: ""
            current_function: None
            inside_defer: false
            //TODO: use program.loaded_modules
            debug_info: CodegenDebugInfo(
                compiler
                line_spans: [:]
                statement_span_comments: debug_info
            )
            namespace_stack: []
            fresh_var_counter: 0
            fresh_label_counter: 0
        )

        mut result: [String:(String, String)] = [:]

        // Unified forwarding header
        mut output = ""
        output += "#pragma once\n"
        output += "#include <lib.h>\n"
        output += "#ifdef _WIN32\n"
        output += "extern \"C\" __cdecl int SetConsoleOutputCP(unsigned int code_page);\n"
        output += "const unsigned int CP_UTF8 = 65001;\n"
        output += "#endif\n"

        let sorted_modules = generator.topologically_sort_modules()
        mut imported_paths: [String:(String, String)] = [:] // path -> (before, after)
        for idx in sorted_modules.size()..0 {
            let i = sorted_modules[idx - 1]
            mut q = Queue<ScopeId>()
            q.enqueue(ScopeId(module_id: i, id: 0))
            while not q.is_empty() {
                let scope_id = q.dequeue()
                let scope = generator.program.get_scope(scope_id)
                for child in scope.children {
                    q.enqueue(child)
                }
                if scope.import_path_if_extern is Some(path) {
                    mut before = ""
                    mut after = ""

                    for action in scope.before_extern_include {
                        match action {
                            Define(name, value) => {
                                before += format("#ifdef {}\n", name)
                                before += format("#undef {}\n", name)
                                before += "#endif\n"
                                before += format("#define {} {}\n", name, value)
                            }
                            Undefine(name) => {
                                before += format("#ifdef {}\n", name)
                                before += format("#undef {}\n", name)
                                before += "#endif\n"
                            }
                        }
                    }

                    for action in scope.after_extern_include {
                        match action {
                            Define(name, value) => {
                                after += format("#ifdef {}\n", name)
                                after += format("#undef {}\n", name)
                                after += "#endif\n"
                                after += format("#define {} {}\n", name, value)
                            }
                            Undefine(name) => {
                                after += format("#ifdef {}\n", name)
                                after += format("#undef {}\n", name)
                                after += "#endif\n"
                            }
                        }
                    }

                    imported_paths.set(path, (before, after))
                }
            }
        }

        for (path, actions) in imported_paths {
            output += actions.0
            output += format("#include <{}>\n", path)
            output += actions.1
        }

        output += "namespace Jakt {\n"

        for idx in sorted_modules.size()..0 {
            let i = sorted_modules[idx - 1].id
            if i == 0 {
                // Skip 0 because it's the prelude
                continue
            }
            let module = generator.program.modules[i]
            generator.compiler.dbg_println(format("generate: module idx: {}, module.name {}", i, module.name))
            let scope_id = ScopeId(module_id: module.id, id: 0)
            let scope = generator.program.get_scope(scope_id)
            output += generator.codegen_namespace_predecl(scope, current_module: module)
        }

        output += "} // namespace Jakt\n"


        // FIXME: Try and get rid of this header if possible.
        result.set(
            "__unified_forward.h",
            (output, compiler.current_file_path()!.to_string()),
        )


        for as_forward in [true, false] {
            for idx in sorted_modules.size()..0 {
                // Module forward declarations header (if as_forward), or module implementation (if not as_forward)
                let i = sorted_modules[idx - 1].id
                if i == 0 {
                    // Skip 0 because it's the prelude
                    continue
                }

                let module = generator.program.modules[i]
                generator.compiler.dbg_println(format("generate: module idx: {}, module.name {} - forward: {}", i, module.name, as_forward))

                let header_name = format("{}.h", module.name)
                let impl_name = format("{}.cpp", module.name)

                if as_forward {
                    output = "#pragma once\n"
                    output += "#include \"__unified_forward.h\"\n"
                } else {
                    output = format("#include \"{}\"\n", header_name)
                }

                let scope_id = ScopeId(module_id: module.id, id: 0)
                let scope = generator.program.get_scope(scope_id)

                if as_forward {
                    for child_scope_id in scope.children {
                        let scope = generator.program.get_scope(child_scope_id)
                        if scope.import_path_if_extern.has_value() {
                            let has_name = scope.namespace_name.has_value()
                            if has_name {
                                output += format("namespace {} {{\n", scope.namespace_name!)
                            }
                            for action in scope.before_extern_include {
                                match action {
                                    Define(name, value) => {
                                        output += format("#ifdef {}\n", name)
                                        output += format("#undef {}\n", name)
                                        output += "#endif\n"
                                        output += format("#define {} {}\n", name, value)
                                    }
                                    Undefine(name) => {
                                        output += format("#ifdef {}\n", name)
                                        output += format("#undef {}\n", name)
                                        output += "#endif\n"
                                    }
                                }
                            }
                            output += format("#include <{}>\n", scope.import_path_if_extern!)
                            for action in scope.after_extern_include {
                                match action {
                                    Define(name, value) => {
                                        output += format("#ifdef {}\n", name)
                                        output += format("#undef {}\n", name)
                                        output += "#endif\n"
                                        output += format("#define {} {}\n", name, value)
                                    }
                                    Undefine(name) => {
                                        output += format("#ifdef {}\n", name)
                                        output += format("#undef {}\n", name)
                                        output += "#endif\n"
                                    }
                                }
                            }
                            if has_name {
                                output += " } // namespace " + scope.namespace_name! + "\n"
                            }
                        }
                    }
                    for id in module.imports {
                        let module = generator.program.modules[id.id]
                        output += format("#include \"{}.h\"\n", module.name)
                    }
                }

                output += "namespace Jakt {\n"

                if not module.is_root {
                    generator.namespace_stack.push(module.name)
                }

                output += generator.codegen_namespace(scope, current_module: module, as_forward)

                if not module.is_root {
                    // FIXME: It's awkward that we need a temporary to avoid the C++ nodiscard warning
                    let dummy = generator.namespace_stack.pop()
                }

                output += generator.deferred_output
                generator.deferred_output = ""
                output += "} // namespace Jakt\n"

                if as_forward {
                    result.set(header_name, (output, module.resolved_import_path))
                } else {
                    result.set(impl_name, (output, module.resolved_import_path))
                }
            }
        }

        for idx in sorted_modules.size()..0 {
            let i = sorted_modules[idx - 1].id

            let module = generator.program.modules[i]

            let header_name = format("{}.h", module.name)
            let impl_name = format("{}_specializations.cpp", module.name)

            if i == 0 {
                output = ""
            } else {
                output = format("#include \"{}\"\n", header_name)
            }
            // FIXME: Figure out exactly which headers we need to include to get all the types we need.
            for id in sorted_modules {
                if id.id == 0 {
                    continue
                }
                let module = generator.program.modules[id.id]
                output += format("#include \"{}.h\"\n", module.name)
            }

            let scope_id = ScopeId(module_id: module.id, id: 0)
            let scope = generator.program.get_scope(scope_id)
            output += "namespace Jakt {\n"
            if not module.is_root {
                generator.namespace_stack.push(module.name)
            }

            output += generator.codegen_namespace_specializations(scope, current_module: module)
            if not module.is_root {
                // FIXME: It's awkward that we need a temporary to avoid the C++ nodiscard warning
                let dummy = generator.namespace_stack.pop()
            }

            output += generator.deferred_output
            generator.deferred_output = ""
            output += "} // namespace Jakt\n"

            result.set(impl_name, (output, module.resolved_import_path))
        }

        return result
    }

    fn postorder_traversal(this, type_id: TypeId, mut visited: {TypeId}, dependency_graph: [TypeId: [TypeId]], mut output: [TypeId]) throws {
        if visited.contains(type_id) {
            return
        }
        visited.add(type_id)
        if dependency_graph.contains(type_id) {
            for dependency in dependency_graph.get(type_id)! {
                .postorder_traversal(type_id: dependency, visited, dependency_graph, output)
            }
        }
        output.push(type_id)
    }

    fn produce_codegen_dependency_graph(this, scope: Scope) throws -> [TypeId:[TypeId]] {
        mut dependency_graph: [TypeId:[TypeId]] = [:]

        for type_ in scope.types {
            dependency_graph.set(type_.1, .extract_dependencies_from(type_id: type_.1, dependency_graph, top_level: true))
        }

        return dependency_graph
    }

    fn extract_dependencies_from(this, type_id: TypeId, dependency_graph: [TypeId:[TypeId]], top_level: bool) -> [TypeId] {
        mut dependencies: [TypeId] = []

        let type_ = .program.get_type(type_id)
        if not type_.is_boxed(program: .program) and dependency_graph.contains(type_id) {
            for dependency in dependency_graph.get(type_id)! {
                dependencies.push(dependency)
            }
            return dependencies
        }

        let inner_dependencies = match type_ {
            Enum(enum_id) => .extract_dependencies_from_enum(enum_id, dependency_graph, top_level)
            GenericEnumInstance(id) => .extract_dependencies_from_enum(enum_id: id, dependency_graph, top_level)
            Struct(id) => .extract_dependencies_from_struct(struct_id: id, dependency_graph, top_level, args: [])
            GenericInstance(id, args) => .extract_dependencies_from_struct(struct_id: id, dependency_graph, top_level, args)
            else => []
        }

        for dependency in inner_dependencies {
            dependencies.push(dependency)
        }

        return dependencies
    }

    fn extract_dependencies_from_enum(this, enum_id: EnumId, dependency_graph: [TypeId:[TypeId]], top_level: bool) -> [TypeId] {
        mut dependencies: [TypeId] = []

        let enum_ = .program.get_enum(enum_id)
        if enum_.definition_linkage is External {
            // This type is defined somewhere else,
            // so we can skip marking it as a dependency.
            return dependencies
        }
        if enum_.is_boxed and not top_level {
            // We store and pass these as pointers, so we don't need to
            // include them in the dependency graph.
            return dependencies
        }
        dependencies.push(enum_.type_id)
        if enum_.underlying_type_id != unknown_type_id() {
            let inner_dependencies = .extract_dependencies_from(type_id: enum_.underlying_type_id, dependency_graph, top_level: false)
            for dependency in inner_dependencies {
                dependencies.push(dependency)
            }
        }
        for variant in enum_.variants {
            match variant {
                Typed(type_id) => {
                    let inner_dependencies = .extract_dependencies_from(type_id, dependency_graph, top_level: false)
                    for dependency in inner_dependencies {
                        dependencies.push(dependency)
                    }
                }
                StructLike(fields) => {
                    for field in fields {
                        let type_id = .program.get_variable(field).type_id
                        let inner_dependencies = .extract_dependencies_from(type_id, dependency_graph, top_level: false)
                        for dependency in inner_dependencies {
                            dependencies.push(dependency)
                        }
                    }
                }
                else => {}
            }
        }

        return dependencies
    }

    fn extract_dependencies_from_struct(
        this
        struct_id: StructId
        dependency_graph: [TypeId:[TypeId]]
        top_level: bool
        args: [TypeId]
    ) -> [TypeId] {
        mut dependencies: [TypeId] = []
        let struct_ = .program.get_struct(struct_id)

        if struct_.definition_linkage is External and struct_.name != "Optional" {
            // This type is defined somewhere else,
            // so we can skip marking it as a dependency.
            return dependencies
        }

        if struct_.record_type is Class and not top_level {
            // We store and pass these as pointers, so we don't need to
            // include them in the dependency graph.
            return dependencies
        }

        if not args.is_empty() {
            for inner_type in args {
                let inner_dependencies = match .program.get_type(inner_type) {
                    Enum | Struct => .extract_dependencies_from(type_id: inner_type, dependency_graph, top_level: false)
                    else => []
                }

                for dependency in inner_dependencies {
                    dependencies.push(dependency)
                }
            }
        }

        mut super_struct_id = struct_.super_struct_id
        while super_struct_id.has_value() {
            let super_struct = .program.get_struct(super_struct_id!)
            let super_dependencies = .extract_dependencies_from_struct(struct_id: super_struct_id!, dependency_graph, top_level: true, args: [])
            dependencies.push_values(&super_dependencies)
            super_struct_id = super_struct.super_struct_id
        }

        dependencies.push(struct_.type_id)
        // The struct's fields are also dependencies.
        for field in struct_.fields {
            let type_id = .program.get_variable(field.variable_id).type_id
            let inner_dependencies = .extract_dependencies_from(type_id, dependency_graph, top_level: false)
            for dependency in inner_dependencies {
                dependencies.push(dependency)
            }
        }
        return dependencies
    }

    fn is_full_respecialization(this, anon type_args: [TypeId]) -> bool {
        for type_id in type_args {
            if .program.get_type(type_id) is TypeVariable {
                return false
            }
        }
        return true
    }

    fn codegen_namespace_specialization(
        mut this
        function_id: FunctionId
        containing_struct: TypeId?
        scope: Scope
        current_module: Module
        define_pass: bool = false
    ) throws -> String? {
        let function = .program.get_function(function_id)
        if not function.function_scope_id.module_id.equals(current_module.id) {
            return None
        }

        if function.is_comptime {
            return None
        }

        let previous_function = .current_function
        .current_function = function
        defer .current_function = previous_function

        if function.linkage is External or
            function.type is ImplicitConstructor or
            function.type is ImplicitEnumConstructor or
            function.type is Destructor {

            return None
        }

        if function.generics.params.is_empty() or not function.specialization_index.has_value() {
            return None
        }

        if containing_struct.has_value() and not match .program.get_type(containing_struct!) {
            Struct(struct_id) => .program.get_struct(struct_id).generic_parameters.is_empty()
            Enum(struct_id) => .program.get_enum(struct_id).generic_parameters.is_empty()
            else => false
        } {
            return None
        }

        // Generate a specialied template, and then do an explicit instantiation of it.
        // This is necessary because we don't want to put the specialization in the header file.

        mut output = ""
        let is_full_respecialization = .is_full_respecialization(
            function.generics.specializations[function.specialization_index!]
        )

        if define_pass and is_full_respecialization {
            output = "template<"
            mut first = true
            for arg in function.generics.specializations[function.specialization_index!] {
                if .program.get_type(arg) is TypeVariable(name) {
                    if first {
                        first = false
                    } else {
                        output += ", "
                    }
                    output += format("typename {}", name)
                }
            }

            output += ">\n"

            output += .codegen_function_in_namespace(
                function
                containing_struct
                skip_template: true
                explicit_generic_instantiation: function.generics.specializations[function.specialization_index!]
            )
        } else if is_full_respecialization {
            mut args: [String] = []
            for arg in function.generics.specializations[function.specialization_index!] {
                args.push(.codegen_type(arg))
            }

            output += format(
                "\n/* specialisation {} of function {}: {} */"
                function.specialization_index!
                function.name
                args
            )
            output += "\ntemplate<> "
            if function.return_type_id == never_type_id() {
                output += "[[noreturn]] "
            }

            output += match function.can_throw {
                true => format("ErrorOr<{}>", .codegen_type(function.return_type_id))
                false => .codegen_type(function.return_type_id)
            }

            output += " "

            let qualifier = match containing_struct.has_value() {
                true => .codegen_type_possibly_as_namespace(type_id: containing_struct!, as_namespace: true)
                false => ""
            }
            if not qualifier.is_empty() {
                output += qualifier
                output += "::"
            }
            output += function.name_for_codegen().as_name_for_definition()

            output += "<"
            mut first = true
            for type_id in function.generics.specializations[function.specialization_index!] {
                if first {
                    first = false
                } else {
                    output += ", "
                }
                output += .codegen_type(type_id)
            }

            output += ">("

            first = true
            for param in function.params {
                let variable = param.variable
                if variable.name == "this" {
                    continue
                }
                if not first {
                    output += ","
                } else {
                    first = false
                }
                let variable_type = .program.get_type(variable.type_id)
                output += .codegen_type(variable.type_id)
                output += " "
                if not variable.is_mutable and not (variable_type is Reference or variable_type is MutableReference) {
                    output += "const "
                }
                output += variable.name_for_codegen().as_name_for_definition()
            }

            output += ")"
            if not function.is_static() and not function.is_mutating() {
                output += " const"
            }
            output += ";\n"
        } else {
            output = format("\n/* specialisation {} of function {} omitted, not fully specialized: {} */\n"
                function.specialization_index!
                function.name
                function.generics.specializations[function.specialization_index!]
            )
        }

        return output
    }

    fn codegen_namespace_specializations(mut this, scope: Scope, current_module: Module) throws -> String {
        if scope.alias_path.has_value() or scope.import_path_if_extern.has_value() {
            return ""
        }
        mut seen_types: {TypeId} = {}

        mut output = ""
        if scope.namespace_name.has_value() {
            output += "namespace " + scope.namespace_name! + " {\n"
        }

        for define_pass in [false, true] {
            for (_, overload_set) in scope.functions {
                for function_id in overload_set {
                    let value = .codegen_namespace_specialization(function_id, containing_struct: None, scope, current_module, define_pass)
                    if value.has_value() {
                        output += value!
                    }
                }
            }

            for (_, struct_id) in scope.structs {
                if not struct_id.module.equals(current_module.id) {
                    continue
                }
                let struct_ = .program.get_struct(struct_id)
                if seen_types.contains(struct_.type_id) {
                    continue
                }

                let scope = .program.get_scope(struct_.scope_id)
                for (_, overload_set) in scope.functions {
                    for function_id in overload_set {
                        let value = .codegen_namespace_specialization(
                            function_id
                            containing_struct: struct_.type_id
                            scope
                            current_module
                            define_pass
                        )
                        if value.has_value() {
                            output += value!
                        }
                    }
                }
            }

            for (_, enum_id) in scope.enums {
                if not enum_id.module.equals(current_module.id) {
                    continue
                }
                let enum_ = .program.get_enum(enum_id)
                if seen_types.contains(enum_.type_id) {
                    continue
                }

                let scope = .program.get_scope(enum_.scope_id)
                for (_, overload_set) in scope.functions {
                    for function_id in overload_set {
                        let value = .codegen_namespace_specialization(
                            function_id
                            containing_struct: enum_.type_id
                            scope
                            current_module
                            define_pass
                        )
                        if value.has_value() {
                            output += value!
                        }
                    }
                }
            }
        }

        for child_scope_id in scope.children {
            let child_scope = .program.get_scope(child_scope_id)
            if child_scope.namespace_name.has_value() {
                let name = child_scope.namespace_name!
                .namespace_stack.push(name)
                output += .codegen_namespace_specializations(scope: child_scope, current_module)
                // FIXME: It's awkward that we need a temporary to avoid the C++ nodiscard warning
                let dummy = .namespace_stack.pop()
            }
        }

        if scope.namespace_name.has_value() {
            output += "}\n"
        }
        return output
    }

    fn codegen_namespace(mut this, scope: Scope, current_module: Module, as_forward: bool) throws -> String {
        if scope.alias_path.has_value() or scope.import_path_if_extern.has_value() {
            return ""
        }
        mut seen_types: {TypeId} = {}

        if as_forward {
            mut output = ""
            if scope.namespace_name.has_value() {
                output += "namespace " + scope.namespace_name! + " {\n"
            }

            let dependency_graph = .produce_codegen_dependency_graph(scope)
            for entry in dependency_graph {
                let traversal: [TypeId] = []
                .postorder_traversal(
                    type_id: entry.0
                    visited: seen_types
                    dependency_graph
                    output: traversal
                )
                for type_id in traversal {
                    let type_ = .program.get_type(type_id)
                    match type_ {
                        Enum(enum_id) => {
                            if not enum_id.module.equals(current_module.id) {
                                // Skip over imports from other modules
                                continue
                            }
                            let enum_ = .program.get_enum(enum_id)
                            let enum_output = .codegen_enum(enum_)
                            if not enum_output.is_empty() {
                                output += enum_output
                            }
                        }
                        Struct(struct_id) => {
                            if not struct_id.module.equals(current_module.id) {
                                // Skip over imports from other modules
                                continue
                            }

                            let struct_ = .program.get_struct(struct_id)
                            let struct_output = .codegen_struct(struct_)

                            if not struct_output.is_empty() {
                                output += struct_output
                            }
                        }
                        Trait => { }
                        else => {
                            panic(format("Unexpected type in dependency graph: {}", type_))
                        }
                    }
                    seen_types.add(type_id)
                }
            }

            for (_, struct_id) in scope.structs {
                if not struct_id.module.equals(current_module.id) {
                    continue
                }
                let struct_ = .program.get_struct(struct_id)
                if seen_types.contains(struct_.type_id) {
                    continue
                }
                output += .codegen_struct(struct_)
                output += "\n"
            }

            for (_, enum_id) in scope.enums {
                if not enum_id.module.equals(current_module.id) {
                    continue
                }
                let enum_ = .program.get_enum(enum_id)
                if seen_types.contains(enum_.type_id) {
                    continue
                }
                output += .codegen_enum(enum_)
                output += "\n"
            }

            for (_, overload_set) in scope.functions {
                for function_id in overload_set {
                    let function = .program.get_function(function_id)
                    if not function.function_scope_id.module_id.equals(current_module.id) {
                        continue
                    }

                    if function.is_comptime {
                        continue
                    }

                    let previous_function = .current_function
                    .current_function = function
                    defer .current_function = previous_function

                    if function.linkage is External or
                        function.type is ImplicitConstructor or
                        function.type is ImplicitEnumConstructor or
                        function.type is Destructor {

                        continue
                    }

                    if function.generics.params.is_empty() {
                        continue
                    }

                    if not function.specialization_index.has_value() {
                        output += .codegen_function_predecl(function, allow_generics: true)
                    }
                }
            }

            for child_scope_id in scope.children {
                let child_scope = .program.get_scope(child_scope_id)
                if child_scope.namespace_name.has_value() {
                    let name = child_scope.namespace_name!
                    .namespace_stack.push(name)
                    output += .codegen_namespace(scope: child_scope, current_module, as_forward)
                    // FIXME: It's awkward that we need a temporary to avoid the C++ nodiscard warning
                    let dummy = .namespace_stack.pop()
                }
            }

            if scope.namespace_name.has_value() {
                output += "}\n"
            }
            return output
        }

        mut output = ""
        if scope.namespace_name.has_value() {
            output += "namespace " + scope.namespace_name! + " {\n"
        }

        for (_, overload_set) in scope.functions {
            for function_id in overload_set {
                let function = .program.get_function(function_id)
                if not function.function_scope_id.module_id.equals(current_module.id) {
                    continue
                }

                if function.is_comptime {
                    continue
                }

                let previous_function = .current_function
                .current_function = function
                defer .current_function = previous_function

                if function.linkage is External or
                    function.type is ImplicitConstructor or
                    function.type is ImplicitEnumConstructor or
                    function.type is Destructor {

                    continue
                }

                if function.generics.params.is_empty() {
                    output += .codegen_function(function)
                    output += "\n"
                }
            }
        }

        for (_, struct_id) in scope.structs {
            if not struct_id.module.equals(current_module.id) {
                continue
            }
            let struct_ = .program.get_struct(struct_id)

            if struct_.definition_linkage is External {
                continue
            }

            if not struct_.generic_parameters.is_empty() {
                continue
            }

            output += .codegen_debug_description_getter(struct_)

            let scope = .program.get_scope(struct_.scope_id)
            for (_, overload_set) in scope.functions {
                for function_id in overload_set {
                    let function = .program.get_function(function_id)
                    if function.force_inline is MakeDefinitionAvailable {
                        continue
                    }

                    let previous_function = .current_function
                    .current_function = function
                    defer .current_function = previous_function

                    if function.type is ImplicitConstructor {
                        output += .codegen_constructor(function)
                        output += "\n"
                    } else if function.type is Destructor {
                        output += .codegen_destructor(&struct_, &function, is_inline: false)
                        output += "\n"
                    } else if not function.type is ImplicitEnumConstructor and
                        not function.is_comptime and
                        function.generics.params.is_empty() {

                        output += .codegen_function_in_namespace(function, containing_struct: struct_.type_id)
                        output += "\n"
                    }
                }
            }
        }

        for (_, enum_id) in scope.enums {
            if not enum_id.module.equals(current_module.id) {
                continue
            }
            let enum_ = .program.get_enum(enum_id)

            if enum_.definition_linkage is External {
                continue
            }

            if not enum_.generic_parameters.is_empty() {
                continue
            }

            if enum_.underlying_type_id == void_type_id() {
                output += .codegen_enum_debug_description_getter(enum_)
                let (common_fields, variant_field_list) = .codegen_enum_field_lists(enum_)
                output += .codegen_enum_constructors(
                    enum_
                    is_inside_struct: false
                    generic_parameter_list: None
                    variant_field_list
                    common_fields
                )

                output += format("{0}::~{0}()", enum_.name)

                .codegen_enum_destructor_body(enum_, &mut output)

                output += format("void {0}::__jakt_destroy_variant() {{\n", enum_.name)
                .codegen_enum_destroy_variant(enum_, &mut output)
                output += "}\n"
            }

            let scope = .program.get_scope(enum_.scope_id)
            for (_, overload_set) in scope.functions {
                for function_id in overload_set {
                    let function = .program.get_function(function_id)
                    let previous_function = .current_function
                    .current_function = function
                    defer .current_function = previous_function

                    if not function.type is ImplicitConstructor and
                        not function.type is ImplicitEnumConstructor and
                        not function.type is Destructor and
                        not function.is_comptime and
                        function.generics.params.is_empty() {

                        output += .codegen_function_in_namespace(function, containing_struct: enum_.type_id)
                        output += "\n"
                    }
                }
            }
        }

        for child_scope_id in scope.children {
            let child_scope = .program.get_scope(child_scope_id)
            if child_scope.namespace_name.has_value() {
                let name = child_scope.namespace_name!
                .namespace_stack.push(name)
                output += .codegen_namespace(scope: child_scope, current_module, as_forward)
                // FIXME: It's awkward that we need a temporary to avoid the C++ nodiscard warning
                let dummy = .namespace_stack.pop()
            }
        }

        if scope.namespace_name.has_value() {
            output += "}\n"
        }
        return output
    }

    fn codegen_namespace_predecl(mut this, scope: Scope, current_module: Module) throws -> String {
        if scope.alias_path.has_value() or scope.import_path_if_extern.has_value() {
            return ""
        }
        mut output = ""
        if scope.namespace_name.has_value() {
            output += "namespace "
            output += scope.namespace_name!
            output += " {\n"
        }
        for (_, struct_id) in scope.structs {
            if not struct_id.module.equals(current_module.id) {
                continue
            }
            let struct_ = .program.get_struct(struct_id)
            output += .codegen_struct_predecl(struct_)
            output += "\n"
        }

        for (_, enum_id) in scope.enums {
            if not enum_id.module.equals(current_module.id) {
                continue
            }
            let enum_ = .program.get_enum(enum_id)
            output += .codegen_enum_predecl(enum_)
            output += "\n"
        }

        for child in scope.children {
            output += .codegen_namespace_predecl(scope: .program.get_scope(child), current_module)
        }

        for (_, overload_set) in scope.functions {
            for function_id in overload_set {
                if not function_id.module.equals(current_module.id) {
                    continue
                }
                let function = .program.get_function(function_id)
                if function.type is ImplicitEnumConstructor {
                    continue
                }

                let previous_function_id = .current_function
                .current_function = Some(function)
                defer .current_function = previous_function_id

                if not function.type is ImplicitConstructor and
                    not function.type is Destructor and
                    function.name_for_codegen().as_name_for_use() != "main" and
                    function.generics.params.is_empty() {

                    output += .codegen_function_predecl(function)
                    output += "\n"
                }
            }
        }

        if scope.namespace_name.has_value() {
            output += "}\n"
        }
        return output
    }

    fn codegen_template_parameter_names(
        mut this
        anon parameters: [FunctionGenericParameter]
        names: &mut [String]
    ) throws -> String {
        mut ids: [TypeId] = []
        for parameter in parameters {
            ids.push(parameter.checked_parameter.type_id)
        }
        return .codegen_template_parameter_names(ids, names)
    }

    fn codegen_template_parameter_names(
        mut this
        anon parameters: [FunctionGenericParameter]
    ) throws -> String {
        mut names: [String] = []
        return .codegen_template_parameter_names(parameters, &mut names)
    }

    fn codegen_template_parameter_names(
        mut this
        anon parameters: [CheckedGenericParameter]
        names: &mut [String]
    ) throws -> String {
        mut ids: [TypeId] = []
        for parameter in parameters {
            ids.push(parameter.type_id)
        }
        return .codegen_template_parameter_names(ids, names)
    }

    fn codegen_template_parameter_names(
        mut this
        anon parameters: [CheckedGenericParameter]
    ) throws -> String {
        mut names: [String] = []
        return .codegen_template_parameter_names(parameters, &mut names)
    }

    fn codegen_template_parameter_names(
        mut this
        anon parameters: [TypeId]
        names: &mut [String]
    ) throws -> String {
        mut output = ""
        mut first = true
        for id in parameters {
            if first {
                first = false
            } else {
                output += ","
            }
            if .program.get_type(id) is TypeVariable(is_value) and is_value {
                output += "auto "
            } else {
                output += "typename "
            }
            let name = .codegen_type(id)

            output += name
            names.push(name)
        }
        return output
    }

    fn codegen_function_generic_parameters(mut this, function: CheckedFunction) throws -> String {
        mut output = ""

        if not function.generics.params.is_empty() {
            output += "template <"
            output += .codegen_template_parameter_names(function.generics.params)
            output += ">\n"
        }

        return output
    }

    fn codegen_function_predecl(mut this, function: CheckedFunction, as_method: bool = false, allow_generics: bool = false) throws -> String {
        mut output = ""

        if (allow_generics or not function.generics.params.is_empty()) and function.linkage is External {
            return ""
        }

        if function.is_comptime {
            return ""
        }

        if function.force_inline is ForceInline {
            return ""
        }

        // FIXME: for now, just exit early if we're a constructor
        if function.type is ImplicitConstructor {
            return ""
        }

        if function.linkage is External {
            output += "extern "
        }

        output += .codegen_function_generic_parameters(function)

        if function.return_type_id == never_type_id() {
            output += "[[noreturn]] "
        }

        if function.name_for_codegen().as_name_for_definition() == "main" {
            output += "ErrorOr<int>"
        } else {
            if as_method and function.is_static() {
                output += "static "
            }

            if function.is_virtual {
                output += "virtual "
            }

            let naked_return_type = .codegen_type(function.return_type_id)
            let return_type = match function.can_throw {
                true => format("ErrorOr<{}>", naked_return_type)
                false => naked_return_type
            }
            output += return_type
        }

        output += " "
        output += function.name_for_codegen().as_name_for_definition()
        output += "("

        mut first = true
        for param in function.params {
            if first and param.variable.name == "this" {
                continue
            }

            if first {
                first = false
            } else {
                output += ", "
            }

            let param_type = .program.get_type(param.variable.type_id)
            output += .codegen_type(param.variable.type_id)
            output += " "
            if not param.variable.is_mutable and not (param_type is Reference or param_type is MutableReference) {
                output += "const "
            }
            output += param.variable.name_for_codegen().as_name_for_definition()
        }
        output += ")"

        if not function.is_static() and not function.is_mutating() {
            output += " const"
        }
        if function.is_override {
            output += " override"
        }

        output += ";"

        output += "\n"

        return output
    }

    fn codegen_struct_predecl(mut this, struct_: CheckedStruct) throws -> String {
        if struct_.definition_linkage is External {
            return ""
        }
        mut output = ""

        if not struct_.generic_parameters.is_empty() {
            output += "template <"
            output += .codegen_template_parameter_names(struct_.generic_parameters)
            output += ">"
        }

        output += match struct_.record_type {
            Class => "class "
            Struct => "struct "
            else => ""
        }

        output += struct_.name_for_codegen().as_name_for_definition()
        output += ";"

        return output
    }

    fn codegen_struct(mut this, struct_: CheckedStruct) throws -> String {
        mut output = ""

        if struct_.definition_linkage is External {
            return ""
        }

        mut struct_is_generic = false
        mut generic_parameter_names: [String] = []
        mut template_parameters = ""
        if not struct_.generic_parameters.is_empty() {
            struct_is_generic = true
            template_parameters = .codegen_template_parameter_names(
                struct_.generic_parameters
                names: &mut generic_parameter_names
            )
            output += format("template <{}>", template_parameters)
        }

        match struct_.record_type {
            Class => {
                mut class_name_with_generics = ""
                class_name_with_generics += struct_.name_for_codegen().as_name_for_definition()
                mut first = true
                for generic_parameter in struct_.generic_parameters {
                    if not first {
                        class_name_with_generics += ", "
                    } else {
                        class_name_with_generics += "<"
                        first = false
                    }
                    class_name_with_generics += .codegen_type(generic_parameter.type_id)
                }
                if not struct_.generic_parameters.is_empty() {
                    class_name_with_generics += ">"
                }

                output += format("class {} :", struct_.name_for_codegen().as_name_for_definition())
                if struct_.super_struct_id.has_value() {
                    output += format("public {}", .codegen_struct_type(id: struct_.super_struct_id!, as_namespace: true))
                    if not (.program.get_struct(struct_.super_struct_id!).record_type is Class) {
                        output += format(
                            ", public RefCounted<{}>, public Weakable<{}>"
                            class_name_with_generics
                            class_name_with_generics
                        )
                    }
                } else {
                    output += format(
                        "public RefCounted<{}>, public Weakable<{}> "
                        class_name_with_generics
                        class_name_with_generics
                    )
                }

                output += "{\n"

                let scope = .program.get_scope(struct_.scope_id)
                mut has_destructor = false
                for (_, function_ids) in scope.functions {
                    for function_id in function_ids {
                        let function = .program.get_function(function_id)
                        if function.type is Destructor {
                            has_destructor = true
                        }
                    }
                }
                if not has_destructor {
                    output += "  public:\n"
                    output += format("virtual ~{}() = default;\n", struct_.name_for_codegen().as_name_for_definition())
                }
            }
            Struct => {
                output += format("struct {}", struct_.name_for_codegen().as_name_for_definition())
                if struct_.super_struct_id.has_value() {
                    output += format(": public {}", .codegen_struct_type(id: struct_.super_struct_id!, as_namespace: true))
                }
                output += " {\n"
                output += "  public:\n"
            }
            SumEnum => {
                todo("codegen_struct SumEnum")
            }
            ValueEnum => {
                todo("codegen_struct ValueEnum")
            }
            else => {}
        }

        let set_access_level = fn[&mut output](anon visibility: CheckedVisibility) throws -> void => match visibility {
            Restricted | Public => {
                output += "public: "
            }
            Private => {
                output += "private: "
            }
        }

        for field in struct_.fields {
            let variable = .program.get_variable(field.variable_id)
            set_access_level(variable.visibility)
            output += .codegen_type(variable.type_id)
            output += " "
            output += variable.name_for_codegen().as_name_for_definition()
            output += ";"
        }

        let scope = .program.get_scope(struct_.scope_id)
        for (_, overload_set) in scope.functions {
            for function_id in overload_set {
                let previous_function_id = .current_function

                let function = .program.get_function(function_id)
                set_access_level(function.visibility)
                .current_function = Some(function)
                defer .current_function = previous_function_id

                if function.type is Destructor {
                    if struct_.generic_parameters.is_empty() {
                        output += .codegen_destructor_predecl(&struct_)
                    } else {
                        output += .codegen_destructor(&struct_, &function, is_inline: true)
                    }
                    output += "\n"
                } else if function.type is ImplicitConstructor {
                    if struct_.generic_parameters.is_empty() {
                        output += .codegen_constructor_predecl(function)
                    } else {
                        output += .codegen_constructor(function, is_inline: true)
                    }
                    output += "\n"
                } else if function.force_inline is MakeDefinitionAvailable {
                    output += .codegen_function(function, as_method: true)
                } else if struct_is_generic {
                    if not function.specialization_index.has_value() {
                        output += .codegen_function(function, as_method: true)
                    }
                } else if function.generics.params.is_empty() or not function.specialization_index.has_value() {
                        output += .codegen_function_predecl(function, as_method: true)
                }
            }
        }

        set_access_level(CheckedVisibility::Public)
        if struct_.generic_parameters.is_empty() {
            output += "ErrorOr<ByteString> debug_description() const;\n"
        } else {
            output += .codegen_debug_description_getter(struct_, is_inline: true)
        }

        output += "};"

        .deferred_output += .codegen_ak_formatter(
            name: struct_.name_for_codegen().as_name_for_definition()
            generic_parameter_names
            template_parameters
        )

        return output
    }

    fn codegen_enum_predecl(mut this, enum_: CheckedEnum) throws -> String {
        mut output = ""

        if enum_.underlying_type_id != void_type_id() {
            if .program.is_integer(enum_.underlying_type_id) {
                return format("enum class {}: {};", enum_.name, .codegen_type(enum_.underlying_type_id))
            } else {
                todo("Enums with a non-integer underlying type")
            }
        }

        let is_generic = not enum_.generic_parameters.is_empty()
        mut template_args = .codegen_template_parameter_names(enum_.generic_parameters)

        if is_generic {
            output += format("template<{}>\n", template_args)
        }
        output += format("struct {};\n", enum_.name)

        return output
    }

    fn codegen_enum(mut this, enum_: CheckedEnum) throws -> String {
        mut output = ""
        if enum_.underlying_type_id != void_type_id() {
            if .program.is_integer(enum_.underlying_type_id) {
                output += "enum class " + enum_.name + ": " + .codegen_type(enum_.underlying_type_id) + " {\n"
                for variant in enum_.variants {
                    output += match variant {
                        WithValue(name, expr) => name + " = " + .codegen_expression(expr) + ",\n"
                        else => {
                            todo(format("codegen_enum can't generate variant: {}", variant))
                        }
                    }
                }
                return output + "};\n"
            } else {
                todo("Enums with a non-integer underlying type")
            }
        }

        let is_generic = not enum_.generic_parameters.is_empty()
        mut generic_parameter_names: [String] = []
        mut template_args = .codegen_template_parameter_names(enum_.generic_parameters, names: &mut generic_parameter_names)


        let generic_parameter_list = join(generic_parameter_names, separator: ", ")

        if is_generic {
            output += "template<" + template_args + ">\n"
        }
        output += format("struct {}", enum_.name)
        if enum_.is_boxed {
            output += format(": public RefCounted<{}", enum_.name)
            if is_generic {
                output += format("<{}>", join(generic_parameter_names, separator: ", "))
        }
            output += ">"
        }
        output += " {\n"

        // Choose the index type so that it can hold `variants.size()`. The
        // indices go from [0, variants.size() - 1], but for the move
        // constructor we need `__jakt_variant_index`es to be different if one
        // of them is uninitialized.
        // The uninit value is zero.
        // Although there is no practical need for this extra "index" (the same
        // codegen has to provide safe constructors), it is useful for further
        // development on this portion of the codegen, since it enables
        // assertions to verify that the compiler is indeed providing
        // initialized values.
        let max_index_value = enum_.variants.size()

        let index_type = match max_index_value {
            // 0..(1 << 8)
            0uz..256uz => "u8"
            // (1 << 8)..(1 << 16)
            256uz..65536uz => "u16"
            // (1 << 16)..(1 << 32)
            65536uz..4294967296uz => "u32"
            else => "size_t"
        }

        let (common_fields, variant_field_list) = .codegen_enum_field_lists(enum_)

        // Note that all fields are public; this is a struct that is defined by
        // the codegen and is to only be used by Jakt code, which this codegen
        // is the only one in charge of. Using the member directly removes any
        // need to create functions to get the appropiate values.
        // We'll use a getter for the variant index, so that the outputted C++ doesn't
        // have implicit "off-by-one" calculations.


        output += format("{} __jakt_variant_index = 0;\n", index_type, max_index_value)

        if not common_fields.is_empty() {
            // Another maybe-uninitialized data member for the common fields.
            // NOTE: Could have a record of which fields won't have a default constructor
            // so this doesn't output a complex union if all the fields have default constructors.
            output += "union CommonData {\n"

            // This one has to be the first since C++ will use the first field to zero-init
            // a union. This way we guarantee that the union can be default initialized.
            output += "u8 __jakt_uninit_common;\n"

            output += "struct {\n"

            // Make common fields available
            for (field_name, type) in common_fields {
                output += format("{} {};\n", type, field_name)
            }

            output += "} init_common;\n"

            // See comment below about the constructors of `VariantData`.
            // Make sure everything's at zero value.
            output += "constexpr CommonData() {}\n"
            output += "~CommonData() {}\n"

            output += "} common;\n"
        }


        output += "union VariantData {\n"

        // We need to have some way to initialize the union if any of the other fields is non default constructible.
        output += "u8 __jakt_uninit_value;\n"

        for variant_index in 0..enum_.variants.size() {
            let variant = enum_.variants[variant_index]
            let fields: [(String, String)] = variant_field_list[variant_index].1

            if not fields.is_empty() {
                output += "struct {\n"
                for (field_name, field_type) in fields.iterator() {
                    output += format("{} {};\n", field_type, field_name)
                }
                output += format("}} {};\n", variant.name())
            }
        }

        output += "constexpr VariantData() {}\n"
        // make the union's destructor do nothing so C++ can shut up about the union
        // not having a default destructor. The safe destructor is the outer
        // struct's destructor, which has access to the variant index.
        output += "~VariantData() {}\n"

        output += "} as;\n"
        output += format("constexpr {} __jakt_init_index() const noexcept {{ return __jakt_variant_index - 1; }}", index_type)


        if enum_.generic_parameters.is_empty() {
            output += "ErrorOr<ByteString> debug_description() const;\n"
        } else {
            output += .codegen_enum_debug_description_getter(enum_, is_inline: true)
        }



        if is_generic {
            // produce constructors that initialize the correct parts of the union

            output += .codegen_enum_constructors(
                enum_
                is_inside_struct: true
                generic_parameter_list
                variant_field_list
                common_fields
            )

            output += format("~{}()", enum_.name)

            .codegen_enum_destructor_body(enum_, &mut output)

            output += "private: void __jakt_destroy_variant() {\n"
            .codegen_enum_destroy_variant(enum_, &mut output)
            output += "}\n"
            output += "public:\n"

        } else {

            // Just make forward declarations for the constructor.
            for (variant_name, variant_fields) in variant_field_list {
                codegen_enum_constructor_decl(
                    enum_name: enum_.name
                    variant_name
                    variant_fields
                    common_fields
                    is_inline: true
                    ctor_result_type: enum_constructor_result_type(enum_)
                    &mut output
                )
                output += ";\n"
            }


            // Forward declare destructor, move assignment and copy assignment
            // operators/constructors.
            output += format("~{}();\n", enum_.name)
            output += format("{0}& operator=({0} const &);\n", enum_.name)
            output += format("{0}& operator=({0} &&);\n", enum_.name)
            output += format("{0}({0} const&);\n", enum_.name)
            output += format("{0}({0} &&);\n", enum_.name)
            output += "private: void __jakt_destroy_variant();\n"
            output += "public:\n"
        }


        let enum_scope = .program.get_scope(enum_.scope_id)
        for (_, overload_set) in enum_scope.functions {
            for function_id in overload_set {
                let function = .program.get_function(function_id)

                let previous_function_id = .current_function
                .current_function = Some(function)
                defer .current_function = previous_function_id

                guard not function.type is ImplicitEnumConstructor else {
                    continue
                }

                if not enum_.generic_parameters.is_empty() {
                    output += .codegen_function(function, as_method: true)
                } else if function.generics.params.is_empty() or not function.specialization_index.has_value() {
                    output += .codegen_function_predecl(function, as_method: true)
                }
            }
        }

        // The only private thing is the default constructor, which is
        // overriden to allow the `static` functions to start with an undefined
        // value and write to it.
        output += format("private:\n{}() {{}};\n", enum_.name)

        output += "};\n"

        .deferred_output += .codegen_ak_formatter(name: enum_.name, generic_parameter_names, template_args)

        return output
    }

    fn codegen_enum_constructor_decl(
        enum_name: String
        variant_name: String
        variant_fields: [(String, String)] // field_name, .codegen_type(field_type)
        common_fields: [(String, String)] // field_name, .codegen_type(field_type)
        is_inline: bool // whether it's an inline definition (inside of the `struct`) or out-of-line one.
        ctor_result_type: String
        output: &mut String
    ) throws {
        output += "[[nodiscard]] "
        if is_inline {
            output += format("static {} {}(", ctor_result_type, variant_name)
        } else {
            output += format("{} {}::{}(", ctor_result_type, enum_name, variant_name)
        }

        let has_common_fields = not common_fields.is_empty()

        // arguments
        if has_common_fields {
            let (first_name, first_type) = common_fields[0]
            output += format("{} {}", first_type, first_name)

            for i in 1..common_fields.size() {
                let (name, type) = common_fields[i]
                output += format(", {} {}", type, name)
            }

            for (name, type) in variant_fields {
                output += format(", {} {}", type, name)
            }
        } else if not variant_fields.is_empty() {
            let (first_name, first_type) = variant_fields[0]
            output += format("{} {}", first_type, first_name)

            for i in 1..variant_fields.size() {
                let (name, type) = variant_fields[i]
                output += format(", {} {}", type, name)
            }
        }

        output += ")"
    }

    fn codegen_for_enum_variants(this, anon enum_: &CheckedEnum, anon stmt_fmt: &fn(accessor: String) throws -> String, index_expr: String, output: &mut StringBuilder) throws {
        // Adding to output here in a loop was causing a 30% slowdown, so using
        // a StringBuilder to mitigate it.
        output.append(format("switch ({}) {{\n", index_expr))

        let common_field_count = enum_.fields.size()

        for variant_index in 0..enum_.variants.size() {
            let variant = &enum_.variants[variant_index]
            let variant_name = variant.name()
            output.append(format("case {} /* {} */:\n", variant_index, variant_name))

            match variant {
                Untyped | WithValue => {}
                Typed => {
                    output.append(stmt_fmt(accessor: format("as.{}.value", variant_name)))
                }
                StructLike(fields) => {
                    for field in fields[common_field_count..] {
                        let variable = .program.get_variable(field)
                        let name = variable.name_for_codegen().as_name_for_use()

                        output.append(stmt_fmt(accessor: format("as.{}.{}", variant_name, name)))
                    }
                }
            }

            output.append("break;\n")
        }

        output.append("}\n")
    }

    /// Generates the body for a copy/ move assignment operator/constructor.
    ///
    /// Assumes that the argument is named "rhs".
    fn codegen_enum_assignment_body(
        this
        enum_: CheckedEnum
        is_constructor: bool // whether it's a constructor. If false, it's an assignment operator.
        use_move: bool // whether to use the "move()" function from AK lib.
    ) throws -> String {
        mut output = StringBuilder::create()
        output.append("{")

        if is_constructor {
            // The assigned value must be initialized.
            // The constructed value is assumed to be uninitialized.
            output.append("VERIFY(rhs.__jakt_variant_index != 0);\n")
        } else {
            // Both values are assumed to be initialized
            output.append("VERIFY(this->__jakt_variant_index != 0 && rhs.__jakt_variant_index != 0);\n")
        }

        let assign = fn[use_move](accessor: String) throws -> String {
            let rhs = match use_move {
                true => format("move(rhs.{})", accessor)
                false => format("rhs.{}", accessor)
            }
            return format("this->{} = {};\n", accessor, rhs)
        }

        let placement_new = fn[use_move](accessor: String) throws -> String {
            let rhs = match use_move {
                true => format("move(rhs.{})", accessor)
                false => format("rhs.{}", accessor)
            }
            return format("new (&this->{0}) (decltype(this->{0}))({1});\n",
                accessor, rhs)
        }

        if is_constructor {
            for common in enum_.fields {
                let variable = .program.get_variable(common.variable_id)
                let name = variable.name_for_codegen().as_name_for_use()

                output.append(placement_new(accessor: format("common.init_common.{}", name)))
            }
        } else {
            for common in enum_.fields {
                let variable = .program.get_variable(common.variable_id)
                let name = variable.name_for_codegen().as_name_for_use()

                output.append(assign(accessor: format("common.init_common.{}", name)))
            }
        }


        if is_constructor {
            // In a constructor, everything needs placement new since it's uninitialized.
            .codegen_for_enum_variants(&enum_, &placement_new, index_expr: "rhs.__jakt_init_index()", &mut output)
        } else {
            // If it's not the same thing, the only safe thing here is to call the destructor
            // and use placement_new.
            // NOTE: This method can leave performance on the table. Consider the
            // case of two distinct variants where a value of the same type exists
            // at the same offset in both of them. That value could have used an
            // assignment operator since it's already initialized to the correct
            // type.

            output.append("if (this->__jakt_variant_index != rhs.__jakt_variant_index) {\n")
            output.append("this->__jakt_destroy_variant();\n")

            .codegen_for_enum_variants(&enum_, &placement_new, index_expr: "rhs.__jakt_init_index()", &mut output)

            output.append("} else {\n")

            // Here we can use assignment for all of the fields, since we know for sure
            // they have been initialized.

            .codegen_for_enum_variants(&enum_, &assign, index_expr: "rhs.__jakt_init_index()", &mut output)

            output.append("}\n")
        }

        output.append("this->__jakt_variant_index = rhs.__jakt_variant_index;\n")

        output.append("}\n")
        return output.to_string()
    }

    fn codegen_enum_constructors(
        this
        enum_: CheckedEnum
        is_inside_struct: bool
        generic_parameter_list: String? // Must be Some if enum is generic.
        variant_field_list: [(String, [(String, String)])] // variant_name, [(field_name, .codegen_type(field_type)]
        common_fields: [(String, String)]
    ) throws -> String {
        mut output = ""

        let is_generic = not enum_.generic_parameters.is_empty()

        let ctor_result_type = enum_constructor_result_type(enum_, generic_parameter_list)

        let ctor_type = match is_generic {
            true => format("{}<{}>", enum_.name, generic_parameter_list)
            false => enum_.name
        }

            // NOTE: these statements should compile because a default constructor is provided privately.
        let (declare_uninit, deref_uninit) = match enum_.is_boxed {
            true => (
                format("NonnullRefPtr<{0}> __jakt_uninit_enum = TRY(adopt_nonnull_ref_or_enomem(new (nothrow) {0}));\n", ctor_type),
                "__jakt_uninit_enum->"
            )
            false => (
                format("{} __jakt_uninit_enum;\n", enum_.name),
                "__jakt_uninit_enum."
            )
        }

        guard (is_inside_struct or not is_generic) else {
            panic("Out of line constructor cannot be generated for generic enum")
        }

        let placement_new = fn[](target: String, name: String) throws -> String {
            return format("new (&{0}) (decltype({1}))(move({1}));\n", target, name)
        }


        // Variant constructors
        for variant_index in 0..variant_field_list.size() {
            let (variant_name, variant_fields) = variant_field_list[variant_index]

            codegen_enum_constructor_decl(
                enum_name: enum_.name
                variant_name
                variant_fields
                common_fields
                is_inline: is_inside_struct
                ctor_result_type
                &mut output
            )

            // Generate the body
            output += "{\n"

            output += declare_uninit

            // +1 because 0 is the uninitialized tag
            output += format("{}__jakt_variant_index = {};\n", deref_uninit, variant_index + 1)

            for field in common_fields {
                let name = field.0
                output += placement_new(
                    target: format("{}common.init_common.{}", deref_uninit, name)
                    name
                )
            }

            for field in variant_fields {
                let name = field.0
                output += placement_new(
                    target: format("{}as.{}.{}", deref_uninit, variant_name, name)
                    name
                )
            }

            output += "return __jakt_uninit_enum;\n"

            output += "}\n"
        }

        // Copy assignment operator
        if is_inside_struct {
            output += format("{0}& operator=({0} const &rhs)", enum_.name)
        } else {
            output += format("{0}& {0}::operator=({0} const &rhs)", enum_.name)
        }
        output += "{\n"
        output += .codegen_enum_assignment_body(enum_,is_constructor: false use_move: false)
        output += "return *this;\n"
        output += "}\n"

        // Copy constructor
        if is_inside_struct {
            output += format("{0}({0} const &rhs)", enum_.name)
        } else {
            output += format("{0}::{0}({0} const &rhs)", enum_.name)
        }
        output += .codegen_enum_assignment_body(enum_, is_constructor: true, use_move: false)

        // Move assignment operator
        if is_inside_struct {
            output += format("{0}& operator=({0} &&rhs)", enum_.name)
        } else {
            output += format("{0}& {0}::operator=({0} &&rhs)", enum_.name)
        }
        output += "{\n"
        output += .codegen_enum_assignment_body(enum_, is_constructor: false, use_move: true)
        output += "return *this;\n"
        output += "}\n"

        // Move constructor
        if is_inside_struct {
            output += format("{0}({0} &&rhs)", enum_.name)
        } else {
            output += format("{0}::{0}({0} &&rhs)", enum_.name)
        }
        output += "{\n"
        output += .codegen_enum_assignment_body(enum_, is_constructor: true, use_move: true)
        output += "}\n"
        return output
    }

    fn codegen_enum_field_lists(mut this, anon enum_: CheckedEnum) throws -> ( [(String, String)], [(String, [(String, String)])]) {
        mut common_fields: [(String, String)] = []
        mut common_field_names: {String} = {}
        for field in enum_.fields {
            let variable = .program.get_variable(field.variable_id)
            let name = variable.name_for_codegen().as_name_for_definition()
            common_fields.push((name, .codegen_type(variable.type_id)))
            common_field_names.add(name)
        }

        // "(<variant_name>, <(arg name, type) list>)". The rest of the function can be generated without looking at the variant.
        mut variant_field_list: [(String, [(String, String)])] = []
        for variant_index in 0..enum_.variants.size() {
            let variant = enum_.variants[variant_index]
            let fields: [(String, String)] = match variant {
                Untyped(name) => [] // no data to add, just the index.
                StructLike(name, fields: own_fields) => {
                    mut fields: [(String, String)] = []
                    for field in own_fields {
                        let variable = .program.get_variable(field)
                        if not common_field_names.contains(variable.name) {
                            fields.push((variable.name_for_codegen().as_name_for_definition(), .codegen_type(variable.type_id)))
                        }
                    }

                    yield fields
                }
                Typed(name, type_id) => {
                    mut fields: [(String, String)] = []
                    fields.push(("value", .codegen_type(type_id)))

                    yield fields
                }
                else => {
                    todo(format("codegen enum variant: {}", variant))
                }
            }

            variant_field_list.push((variant.name(), fields))
        }

        return (common_fields, variant_field_list)
    }

    fn destructor_name(this, anon id: TypeId) -> String? => match .program.get_type(id) {
        JaktString => "ByteString"
        GenericInstance(id) | GenericResolvedType(id) | Struct(id) => {
            let struct_ = .program.get_struct(id)
            yield match (struct_.record_type is Class) {
                true => "NonnullRefPtr"
                false => struct_.name_for_codegen().as_name_for_use()
            }
        }
        TypeVariable(name) => name
        Enum(id) | GenericEnumInstance(id) => {
            let enum_ = .program.get_enum(id)
            yield match enum_.is_boxed {
                true => "NonnullRefPtr"
                false => enum_.name
            }
        }
        else => None
    }

    fn enum_constructor_result_type(enum_: CheckedEnum, generic_parameter_list: String? = None) throws -> String {
        mut result = enum_.name
        if not enum_.generic_parameters.is_empty() {
            result += format("<{}>", generic_parameter_list!)
        }
        if enum_.is_boxed {
            result = format("ErrorOr<NonnullRefPtr<{}>>", result)
        }
        return result
    }

    /// Generates the body for `__jakt_destroy_variant`, which calls the correct
    /// destructors just for the varying part of the enum. Used both in the
    /// destructor to fully destruct everything, but also on copy & move
    /// assignment operators when the variant tag isn't the same.
    fn codegen_enum_destroy_variant(this
        enum_: CheckedEnum
        output: &mut String
    ) throws {
        let common_field_count = enum_.fields.size()
        output += "switch (this->__jakt_init_index()) {\n"
        for variant_index in 0..enum_.variants.size() {
            let variant = enum_.variants[variant_index]
            let variant_name = variant.name()
            output += format("case {} /* {} */:", variant_index, variant_name)

            match variant {
                Untyped | WithValue => {}
                Typed(type_id) => {
                    let ds_name = .destructor_name(type_id)
                    if ds_name is Some(name) {
                        output += format("this->as.{}.value.~{}();\n", variant_name, name)
                    }
                }
                StructLike(fields) => {
                    for field in fields[common_field_count..] {
                        let variable = .program.get_variable(field)

                        let ds: String? = .destructor_name(variable.type_id)

                        if ds is Some(name) {
                            output += format("this->as.{}.{}.~{}();\n",
                                variant_name
                                variable.name_for_codegen().as_name_for_use()
                                name
                            )
                        }

                    }
                }
            }

            output += "break;\n"
        }
        output += "}\n"
    }

    fn codegen_enum_destructor_body(
        this
        enum_: CheckedEnum
        output: &mut String
    ) throws {
        output += "{\n"

        output += "if (this->__jakt_variant_index == 0) return;\n"

        let common_field_count = enum_.fields.size()

        for common_field in enum_.fields {
            let variable = .program.get_variable(common_field.variable_id)

            let ds: String? = .destructor_name(variable.type_id)

            if ds is Some(name) {
                output += format("this->common.init_common.{}.~{}();\n", variable.name_for_codegen().as_name_for_use(), name)
            }

        }

        output += "this->__jakt_destroy_variant();\n"

        output += "}\n"
    }

    fn codegen_debug_description_getter(mut this, struct_: CheckedStruct, is_inline: bool = false) throws -> String {
        mut output = ""

        if not is_inline and not struct_.generic_parameters.is_empty() {
            output += "template <"
            output += .codegen_template_parameter_names(struct_.generic_parameters)
            output += ">\n"
        }

        output += "ErrorOr<ByteString> "
        if not is_inline {
            output += .codegen_type_possibly_as_namespace(type_id: struct_.type_id, as_namespace: true)
            output += "::"
        }
        output += "debug_description() const { "
        output += "auto builder = ByteStringBuilder::create();"
        output += format("TRY(builder.append(\"{}(\"sv));", struct_.name_for_codegen().as_name_for_use())
        output += "{\n"
        output += "JaktInternal::PrettyPrint::ScopedLevelIncrease increase_indent {};\n"

        mut i = 0uz
        for field in struct_.fields {
            let field_var = .program.get_variable(field.variable_id)
            output += "TRY(JaktInternal::PrettyPrint::output_indentation(builder));"
            output += format("TRY(builder.appendff(\"{}: ", field_var.name_for_codegen().as_name_for_use())
            if .program.is_string(field_var.type_id) {
                output += "\\\"{}\\\""
            } else {
                output += "{}"
            }
            if i != struct_.fields.size() - 1 {
                output += ", "
            }
            output += "\", "

            output += match .program.get_type(field_var.type_id) {
                Struct(struct_id) => match .program.get_struct(struct_id).record_type {
                    Class => "*"
                    else => ""
                }
                else => ""
            }
            output += field_var.name_for_codegen().as_name_for_use() + "));\n"
            i++
        }

        output += "}\n"
        output += "TRY(builder.append(\")\"sv));"
        output += "return builder.to_string();"
        output += " }\n"
        return output
    }

    fn codegen_enum_debug_description_getter(mut this, enum_: CheckedEnum, is_inline: bool = false) throws -> String {
        mut output = ""

        if not is_inline and not enum_.generic_parameters.is_empty() {
            output += "template <"
            output += .codegen_template_parameter_names(enum_.generic_parameters)
            output += ">\n"
        }

        output += "ErrorOr<ByteString> "
        if not is_inline {
            output += .codegen_type_possibly_as_namespace(type_id: enum_.type_id, as_namespace: true)
            output += "::"
        }
        output += "debug_description() const {\n"
        output += "auto builder = ByteStringBuilder::create();\n"
        output += "switch (this->__jakt_init_index()) {"

        let common_field_count = enum_.fields.size()

        for i in 0..enum_.variants.size() {
            let variant = enum_.variants[i]
            let name = variant.name()
            output += format("case {} /* {} */: {{\n", i, name)
            match variant {
                StructLike(fields) => {
                    output += format("TRY(builder.append(\"{}::{}\"sv));\n", enum_.name, name)
                    output += format("[[maybe_unused]] auto const& that = this->as.{};\n", name)
                    output += "TRY(builder.append(\"(\"sv));\n"
                    output += "{\n";
                    output += "JaktInternal::PrettyPrint::ScopedLevelIncrease increase_indent {};\n"
                    mut i = 0uz
                    for field in fields {
                        output += "TRY(JaktInternal::PrettyPrint::output_indentation(builder));\n"
                        let var = .program.get_variable(field)
                        if .program.is_string(var.type_id){
                            output += format("TRY(builder.appendff(\"{}: \\\"{{}}\\\"", var.name_for_codegen().as_name_for_use())
                        } else {
                            output += format("TRY(builder.appendff(\"{}: {{}}", var.name_for_codegen().as_name_for_use())
                        }
                        if i != fields.size() - 1 {
                            output += ", "
                        }
                        if i < common_field_count {
                            output += format("\", this->common.init_common.{}));\n", var.name_for_codegen().as_name_for_use())
                        } else {
                            output += format("\", that.{}));\n", var.name_for_codegen().as_name_for_use())
                        }
                        i++
                    }
                    output += "}\n"
                    output += "TRY(builder.append(\")\"sv));\n"
                }
                Typed(type_id) => {
                    output += format("TRY(builder.append(\"{}::{}\"sv));\n", enum_.name, name)
                    output += format("[[maybe_unused]] auto const& that = this->as.{};\n", name)
                    if .program.is_string(type_id){
                        output += "TRY(builder.appendff(\"(\\\"{}\\\")\", that.value));\n"
                    } else {
                        output += "TRY(builder.appendff(\"({})\", that.value));\n"
                    }
                }
                else => {
                    output += format("return ByteString(\"{}::{}\"sv);\n", enum_.name, name)
                }
            }

            output += "break;}\n"
        }

        output += "}\nreturn builder.to_string();\n}\n"

        return output
    }

    fn codegen_ak_formatter(mut this, name: String, generic_parameter_names: [String], anon template_args: String) throws -> String {
        mut output = ""

        let generic_type_args = join(generic_parameter_names, separator: ", ")

        mut qualified_name = ""
        for namespace_ in .namespace_stack {
            qualified_name += format("{}::", namespace_)
        }

        qualified_name += name

        if not generic_parameter_names.is_empty() {
            qualified_name += format("<{}>\n", generic_type_args)
        }

        output += "} // namespace Jakt\n"

        output += format("template<{}>", template_args)
        output += format("struct Jakt::Formatter<Jakt::{}> : Jakt::Formatter<Jakt::StringView>", qualified_name)
        output += "{\n"
        output += format("Jakt::ErrorOr<void> format(Jakt::FormatBuilder& builder, Jakt::{} const& value) {{\n", qualified_name)
        output += "JaktInternal::PrettyPrint::ScopedEnable pretty_print_enable { m_alternative_form };"
        output += "Jakt::ErrorOr<void> format_error = Jakt::Formatter<Jakt::StringView>::format(builder, MUST(value.debug_description()));"
        output += "return format_error;"
        output += "}\n"
        output += "};\n"

        return output + "namespace Jakt {\n"
    }

    fn codegen_expression_and_deref_if_generic_and_needed(
        mut this
        anon expression: CheckedExpression
    ) throws -> String => match .program.get_type(expression.type()) {
        TypeVariable => {
            let contents = .codegen_expression(expression)
            yield format("(*([]<typename V>(V&& value) {{ if constexpr (IsSpecializationOf<V, NonnullRefPtr>) return &*value; else return &value; }})({}))", contents)
        }
        else => .codegen_expression(expression)
    }

    fn codegen_expression(mut this, anon expression: CheckedExpression) throws -> String {
        .generic_inferences = expression.generic_inferences ?? .generic_inferences
        defer .generic_inferences = None

        return match expression {
            Reflect(span) => {
                panic(format("Unexpected reflect expression at {}", span))
            }
            Range(from, to, type_id) => {
                mut output = ""
                let type = .program.get_type(type_id)
                let index_type = match type {
                    GenericInstance(id, args) => args[0]
                    else => {
                        panic("Internal error: range expression doesn't have Range type")
                    }
                }
                output += "("
                output += .codegen_type(type_id)
                output += "{"
                output += "static_cast<"
                output += .codegen_type(index_type)
                output += ">("
                if from.has_value() {
                    output += .codegen_expression(from!)
                } else {
                    output += "0LL"
                }
                output += "),static_cast<"
                output += .codegen_type(index_type)
                output += ">("
                if to.has_value() {
                    output += .codegen_expression(to!)
                } else {
                    output += "9223372036854775807LL"
                }
                output += ")})"
                yield output
            }
            OptionalNone => "JaktInternal::OptionalNone()"
            OptionalSome(expr, type_id) => "static_cast<" + .codegen_type(type_id) + ">(" + .codegen_expression(expr) + ")"
            ForcedUnwrap(expr, type_id) => "(" + .codegen_expression(expr) + ".value())"
            QuotedString(val) => {
                let original_string = val.to_string()
                let escaped_value = original_string.replace(replace: "\n", with: "\\n")
                yield match val.type_id == builtin(BuiltinType::JaktString) {
                    true => "Jakt::ByteString(\"" + escaped_value + "\"sv)"
                    false => {
                        let ids = .program.find_functions_with_name_in_scope(
                            parent_scope_id: .program.find_type_scope_id(val.type_id)
                            function_name: "from_string_literal"
                        )
                        if not ids.has_value() or ids!.is_empty() {
                            panic("Internal error: couldn't find a 'from_string_literal' function despite passing typecheck")
                        }

                        let name = .program.get_function(ids![0]).name_for_codegen().as_name_for_use()
                        let error_handler = match val.may_throw { true => .current_error_handler(), else => "" }
                        yield format(
                            "{}({}::{}(\"{}\"sv))"
                            error_handler
                            .codegen_type(val.type_id)
                            name
                            escaped_value
                        )
                    }
                }
            }
            ByteConstant(val) => "static_cast<u8>(u8'" + val + "')"
            CCharacterConstant(val) => "'" + val + "'"
            CharacterConstant(val) => "static_cast<u32>(U'" + val + "')"
            Var(var) => {
                let name = var.name_for_codegen().as_name_for_use()
                yield match name {
                    "this" => .this_replacement ?? "*this"
                    else => name
                }
            }
            IndexedExpression(expr, index) => "((" + .codegen_expression(expr) + ")[" + .codegen_expression(index) + "])"
            IndexedDictionary(expr, index) => "((" + .codegen_expression(expr) + ")[" + .codegen_expression(index) + "])"
            IndexedTuple(expr, index, is_optional) => match is_optional {
                true => format("(({}).map([](auto& _value) {{ return _value.template get<{}>(); }}))", .codegen_expression(expr), index)
                false => format("(({}).template get<{}>())", .codegen_expression(expr), index)
            }
            IndexedStruct(expr, name, index, is_optional) => {
                mut output = ""
                let object = .codegen_expression(expression: expr)
                output += "(("
                output += object
                output += ")"

                let var_name = match index.has_value() {
                    true => .program.get_variable(index!).name_for_codegen().as_name_for_use()
                    false => name
                }

                let expression_type = .program.get_type(expr.type())
                match expression_type {
                    RawPtr => {
                        output += "->"
                    }
                    Struct(id) | GenericInstance(id) => {
                        let structure = .program.get_struct(id)
                        if structure.record_type is Class and object != "*this" {
                            output += "->"
                        } else {
                            output += "."
                        }
                    }
                    else => {
                        output += "."
                    }
                }
                if is_optional {
                    output += "map([](auto& _value) { return _value"
                    mut access_operator = "."
                    if expression_type is GenericInstance(args) and args.size() > 0 {
                        match .program.get_type(args[0]) {
                            Struct(id) | GenericInstance(id) => {
                                if .program.get_struct(id).record_type is Class {
                                    access_operator = "->"
                                }
                            }
                            else => {}
                        }
                    }
                    output += access_operator
                    output += var_name
                    output += "; })"
                } else {
                    output += var_name
                }
                output += ")"
                yield output
            }
            IndexedCommonEnumMember(expr, index, is_optional) => {
                mut output = ""
                let object = .codegen_expression(expression: expr)
                output += "(("
                output += object
                output += ")"

                match .program.get_type(expr.type()) {
                    RawPtr => {
                        output += "->"
                    }
                    Enum(id) | GenericEnumInstance(id) => {
                        let structure = .program.get_enum(id)
                        if structure.record_type is SumEnum(is_boxed) and is_boxed and object != "*this" {
                            output += "->common.init_common."
                        } else {
                            output += ".common.init_common."
                        }
                    }
                    else => {
                        output += "."
                    }
                }
                if is_optional {
                    output += "map([](auto& _value) { return _value."
                    output += index
                    output += "; })"
                } else {
                    output += index
                }
                output += ")"
                yield output
            }
            ComptimeIndex() => {
                panic("Internal error: ComptimeIndex should have been replaced by now")
            }
            Block(block) => .codegen_block(block)
            Call(call) => .codegen_call(call)
            MethodCall(expr, call, is_optional) => .codegen_method_call(expr, call, is_optional)
            Boolean(val) => match val {
                true => "true"
                else => "false"
            }
            UnaryOp(expr, op, type_id) => {
                mut output = "("
                output += match op {
                    PreIncrement => "++"
                    PreDecrement => "--"
                    Negate => "-"
                    Dereference => match .program.get_type(expr.type()) {
                        RawPtr => "*"
                        else => ""
                    }
                    // FIXME: Remove this once we have proper type mutability support
                    RawAddress => match .program.get_type(expr.type()).is_boxed(program: .program) {
                        true => "const_cast<" + .codegen_type_possibly_as_namespace(type_id, as_namespace: true) + ">(" + "&*"
                        false => "const_cast<" + .codegen_type(type_id) + ">(" + "&"
                    }
                    LogicalNot => "!"
                    BitwiseNot => "~"
                    Sizeof => "sizeof"
                    Is(type_id) => {
                        let is_type = match .program.get_type(type_id) {
                            Struct(id) => {
                                let struct_ = .program.get_struct(id)
                                yield .codegen_namespace_qualifier(scope_id: struct_.scope_id) + struct_.name_for_codegen().as_name_for_use()
                            }
                            else => .codegen_type(type_id)
                        }
                        yield "is<" + is_type + ">("
                    }
                    IsNone => "!"
                    TypeCast(cast) => {
                        mut final_type_id = cast.type_id();
                        let type = .program.get_type(final_type_id)
                        let cast_type = match cast {
                            Fallible => {
                                let type_id = match type {
                                    GenericInstance(args) => args[0]
                                    else => {
                                        panic("Fallible type cast must have Optional result.")
                                    }
                                }
                                mut cast_type = "dynamic_cast"
                                if .program.get_type(type_id) is Struct(struct_id)
                                    and .program.get_struct(struct_id).record_type is Class {
                                        final_type_id = type_id
                                    cast_type = "fallible_class_cast"
                                } else if .program.is_integer(type_id) {
                                    final_type_id = type_id
                                    cast_type = "fallible_integer_cast"
                                }

                                yield cast_type
                            }
                            Infallible => {
                                mut cast_type = "verify_cast"
                                if expr.type() == unknown_type_id() {
                                    cast_type = "assert_type"
                                } else if type.is_boxed(program: .program) {
                                    cast_type = "infallible_class_cast"
                                } else if .program.is_integer(type_id) {
                                    cast_type = "infallible_integer_cast"
                                } else if type is Enum(enum_id) and .program.is_integer(.program.get_enum(enum_id).underlying_type_id) {
                                    cast_type = "infallible_enum_cast"
                                } else if type is RawPtr(inner) {
                                    cast_type = "reinterpret_cast"
                                }
                                yield cast_type
                            }
                        }
                        yield cast_type + "<" + .codegen_type(final_type_id) + ">("
                    }
                    else => ""
                }
                output += "("
                let object = match op {
                    Sizeof(type_id) => .codegen_type(type_id)
                    else => .codegen_expression(expr)
                }
                output += object
                output += match op {
                    PostIncrement => "++)"
                    PostDecrement => "--)"
                    TypeCast | Is => "))"
                    IsEnumVariant(enum_variant, type_id: enum_type_id) => {
                        let name = enum_variant.name()
                        mut suffix = ")"
                        let enum_ = .program.get_enum(match .program.get_type(enum_type_id) {
                            Enum(enum_id) => enum_id
                            GenericEnumInstance(id) => id
                            else => {
                                panic(format("Unexpected type in IsEnumVariant: {}", .program.get_type(enum_type_id)))
                            }
                        })
                        match enum_.record_type {
                            SumEnum => {
                                let is_boxed = enum_.is_boxed
                                if is_boxed and object != "*this"{
                                    suffix += "->"
                                } else {
                                    suffix += "."
                                }

                                // FIXME: This should be a call to find(), but we do not yet provide the needed operator== for that
                                mut variant_index = 0;
                                for variant in enum_.variants {
                                    if variant.name() == name {
                                        break
                                    }
                                    variant_index++
                                }
                                suffix += format("__jakt_init_index() == {} /* {} */", variant_index, name)
                            }
                            ValueEnum => {
                                suffix += format("== {}{}::{}", .codegen_namespace_qualifier(scope_id: enum_.scope_id), enum_.name, name)
                            }
                            else => {
                                panic(format("Unexpected enum record type in IsEnumVariant: {}", enum_.record_type))
                            }
                        }
                        yield suffix
                    }
                    IsSome | IsNone => ").has_value()"
                    RawAddress => "))"
                    else => ")"
                }
                output += ")"
                yield output
            }
            BinaryOp(lhs, rhs, op, type_id) => .codegen_binary_expression(expression, type_id, lhs, rhs, op)
            NumericConstant(val, type_id) => {
                let suffix = match val {
                    I64 => "LL"
                    U64 | USize => "ULL"
                    else => ""
                }

                let type_name = match val {
                    USize => "size_t"
                    else => .codegen_type(type_id)
                }

                yield match val {
                    F32(val)
                    | F64(val)
                    | I8(val)
                    | I16(val)
                    | I32(val)
                    | I64(val)
                    | U8(val)
                    | U16(val)
                    | U32(val)
                    | U64(val)
                    | USize(val)
                    => format("static_cast<{}>({}{})", type_name, val, suffix)
                }
            }
            NamespacedVar(namespaces, var) => {
                mut output = ""
                if var.name_for_codegen().is_scopable() {
                    if var.owner_scope.has_value() {
                        output = .codegen_namespace_qualifier(
                            scope_id: var.owner_scope!
                            skip_current: false
                        )
                    } else {
                        for ns in namespaces {
                            output += ns.name + "::"
                        }
                    }
                }
                yield output + var.name_for_codegen().as_name_for_use()
            }
            Match(expr, match_cases, type_id, all_variants_constant) => .codegen_match(expr, match_cases, type_id, all_variants_constant)
            EnumVariantArg(expr, arg, enum_variant) => {
                let var_name = .codegen_expression(expr)
                let enum_type = .codegen_type_possibly_as_namespace(type_id: expr.type(), as_namespace: true)
                let variant_name = enum_variant.name()
                mut arg_name = "value"
                if enum_variant is StructLike {
                    arg_name = arg.name ?? arg.binding
                }

                let enum_ = .program.get_enum(enum_variant.enum_id())

                let cpp_deref_operator = match enum_.is_boxed {
                    true => "->"
                    else => "."
                }

                // use the common section if the binding uses a common field
                mut section = format("as.{}", variant_name)
                for field in enum_.fields {
                    let field_name = .program.get_variable(field.variable_id).name_for_codegen().as_name_for_use()
                    if field_name == arg_name {
                        section = "common.init_common"
                    }
                }

                yield format("({}){}{}.{}", var_name, cpp_deref_operator, section, arg_name)
            }
            JaktArray(vals, repeat, span, type_id, inner_type_id) => {
                mut output = ""
                if repeat.has_value() {
                    let repeat_val = repeat.value()
                    output += "((DynamicArray<"
                    output += .codegen_type(inner_type_id)
                    output += ">::must_filled("
                    output += .codegen_expression(repeat_val)
                    output += ", "
                    output += .codegen_expression(vals[0])
                    output += ")))"
                } else {
                    output += "((DynamicArray<"
                    output += .codegen_type(inner_type_id)
                    output += ">::must_create_with({"
                    mut first = true
                    for val in vals {
                        if not first {
                            output += ", "
                        } else {
                            first = false
                        }

                        output += .codegen_expression(val)
                    }
                    output += "})))"
                }

                yield output
            }
            JaktDictionary(vals, span, type_id, key_type_id, value_type_id) => {
                mut output = format("({}((Dictionary<{}, {}>::create_with_entries({{",
                    .current_error_handler(),
                    .codegen_type(key_type_id),
                    .codegen_type(value_type_id))

                mut first = true
                for (key, value) in vals {
                    if not first {
                        output += ", "
                    } else {
                        first = false
                    }

                    output += "{"
                    output += .codegen_expression(key)
                    output += ", "
                    output += .codegen_expression(value)
                    output += "}"
                }

                output += "}))))"

                yield output
            }
            JaktSet(vals, span, type_id, inner_type_id) => {
                mut output = ""
                output += format("({}((Set<{}>::create_with_values({{", .current_error_handler(), .codegen_type(inner_type_id))
                mut first = true
                for value in vals {
                    if not first {
                        output += ", "
                    } else {
                        first = false
                    }
                    output += .codegen_expression(value)
                }
                output += "}))))"
                yield output
            }
            JaktTuple(vals, span, type_id) => {
                mut output = ""
                output += "(Tuple{"
                mut first = true
                for val in vals {
                    if not first {
                        output += ", "
                    } else {
                        first = false
                    }

                    output += .codegen_expression(val)
                }
                output += "})"

                yield output
            }
            DependentFunction => {
                .compiler.panic("Dependent functions should have been resolved by now")
            }
            Function(captures, params, can_throw, block, return_type_id, pseudo_function_id) => {
                mut generated_captures: [String] = []
                for capture in captures {
                    generated_captures.push(match capture {
                        ByValue => capture.name
                        AllByReference => "&"
                        else => format("&{}", capture.name)
                    })
                }
                mut generated_params: [String] = []
                for param in params {
                    generated_params.push(format("{} {}", .codegen_type(param.variable.type_id), param.variable.name_for_codegen().as_name_for_definition()))
                }
                let return_type = match can_throw {
                    true => format("ErrorOr<{}>", .codegen_type(return_type_id))
                    false => .codegen_type(return_type_id)
                }

                let last_control_flow = .control_flow_state
                .control_flow_state = last_control_flow.enter_function()
                defer .control_flow_state = last_control_flow

                mut block_output = ""
                if pseudo_function_id.has_value() {
                    let function = .program.get_function(pseudo_function_id!)

                    let previous_function = .current_function
                    .current_function = function
                    defer .current_function = previous_function

                    block_output = .codegen_lambda_block(can_throw, block, return_type_id)
                } else {
                    block_output = .codegen_lambda_block(can_throw, block, return_type_id)
                }

                yield format("[{}]({}) -> {} {}", join(generated_captures, separator: ", "), join(generated_params, separator: ", "), return_type, block_output)
            }
            TryBlock(stmt, error_name, catch_block, span) => {
                mut output = ""
                let try_var = .fresh_var()

                output += "auto "
                output += try_var
                output += " = [&]() -> ErrorOr<void> {"
                let last_control_flow = .control_flow_state
                .control_flow_state.passes_through_match = false
                .control_flow_state.passes_through_try = true
                output += .codegen_statement(statement: stmt)
                output += ";"
                output += "return {};"
                output += "}();\n"

                output += "if ("
                output += try_var
                output += ".is_error()) {"
                if not error_name.is_empty() {
                    output += "auto "
                    output += error_name
                    output += " = "
                    output += try_var
                    output += ".release_error();"
                }
                output += .codegen_block(block: catch_block)
                .control_flow_state = last_control_flow
                output += "}"

                yield output
            }
            Try(expr, catch_block, catch_name, span, type_id, inner_type_id) => {
                mut output = ""
                let fresh_var = .fresh_var()
                let is_void = inner_type_id == void_type_id()
                let try_var = .fresh_var()

                let last_control_flow = .control_flow_state
                .control_flow_state.passes_through_match = false
                .control_flow_state.passes_through_try = true
                defer {
                    .control_flow_state = last_control_flow
                }

                if not is_void {
                    output += "({ Optional<"
                    output += .codegen_type(inner_type_id)
                    output += "> "
                    output += fresh_var
                    output += ";\n"
                }

                output += "auto "
                output += try_var
                output += " = [&]() -> ErrorOr<"
                output += .codegen_type(inner_type_id)
                output += "> { return "
                output += .codegen_expression(expr)
                if is_void {
                    output += ", ErrorOr<void>{}"
                }
                output += "; }();\n"

                if catch_block.has_value() {
                    output += "if ("
                    output += try_var
                    output += ".is_error()) {"
                    if catch_name.has_value() {
                        output += "auto "
                        output += catch_name!
                        output += " = "
                        output += try_var
                        output += ".release_error();\n"
                    }

                    if catch_block!.yielded_type.has_value() {
                        output += fresh_var
                        output += " = ("
                        output += .codegen_block(block: catch_block!)
                        output += ");\n"
                    } else {
                        output += .codegen_block(block: catch_block!)
                    }

                    if not is_void {
                        output += "} else {"
                        output += fresh_var
                        output += " = "
                        output += try_var
                        output += ".release_value();\n"
                    }
                    output += "}\n"
                } else if not is_void {
                    output += "if (!"
                    output += try_var
                    output += ".is_error()) "
                    output += fresh_var
                    output += " = "
                    output += try_var
                    output += ".release_value();\n"
                }

                if not is_void {
                    output += fresh_var
                    if catch_block.has_value() {
                        output += ".release_value()"
                    }
                    output += "; })"
                }

                yield output
            }
            Garbage(span) => {
                todo(format("codegen_expression of bad AST node in {} at {}..{}", this.compiler.get_file_path(span.file_id), span.start, span.end))
            }
        }
    }

    fn codegen_match(mut this, expr: CheckedExpression, match_cases: [CheckedMatchCase], type_id: TypeId, all_variants_constant: bool) throws -> String {
        let last_control_flow = .control_flow_state
        .control_flow_state = .control_flow_state.enter_match()
        mut output = ""

        let expr_type = .program.get_type(expr.type())
        if expr_type is Enum(enum_id) {
            output += .codegen_enum_match(
                enum_: .program.get_enum(enum_id)
                expr
                match_cases
                type_id
                all_variants_constant
            )
        } else {
            output += .codegen_generic_match(
                expr
                cases: match_cases
                return_type_id: type_id
                all_variants_constant
            )
        }
        .control_flow_state = last_control_flow
        return output
    }

    fn codegen_generic_match(mut this, expr: CheckedExpression, cases: [CheckedMatchCase], return_type_id: TypeId, all_variants_constant: bool) throws -> String {
        mut output = ""

        mut is_generic_enum: bool = false
        for case_ in cases {
            if case_ is EnumVariant {
                is_generic_enum = true
                break
            }
        }
        let match_values_all_constant = all_variants_constant and not is_generic_enum

        let cpp_match_result_type = .codegen_type(return_type_id)

        // TODO: Use switch statement if all values are constant
        output += format(
            "([&]() -> JaktInternal::ExplicitValueOrControlFlow<{},{}>"
            cpp_match_result_type,
            .codegen_function_return_type(function: .current_function!)
        ) + "{\n"

        if is_generic_enum {
            output += "auto&& __jakt_enum_value = JaktInternal::deref_if_ref_pointer("
        } else {
            output += "auto __jakt_enum_value = ("
        }
        output += .codegen_expression(expr)
        output += ");\n"

        mut has_default = false
        mut first = true
        for case_ in cases {
            let defaults = case_.defaults

            match case_ {
                EnumVariant(name, args, subject_type_id, scope_id, body) => {
                    let enum_ = .program.get_enum(match .program.get_type(subject_type_id) {
                        Enum(enum_id) => enum_id
                        GenericEnumInstance(id) => id
                        else => {
                            panic(format("Unexpected type in IsEnumVariant: {}", .program.get_type(subject_type_id)))
                        }
                    })
                    // FIXME: This should be a call to find(), but we do not yet provide the needed operator== for that
                    mut variant_index = 0;
                    for variant in enum_.variants {
                        if variant.name() == name {
                            break
                        }
                        variant_index++
                    }

                    output += format("if (__jakt_enum_value.__jakt_init_index() == {} /* {} */) {{\n", variant_index, name)

                    mut variant_type_name = ""
                    let qualifier = .codegen_type_possibly_as_namespace(type_id: subject_type_id, as_namespace: true)
                    if not qualifier.is_empty() {
                        variant_type_name += "typename JaktInternal::RemoveRefPtr<"
                        variant_type_name += qualifier
                        variant_type_name += ">::"
                    }
                    variant_type_name += name

                    if not args.is_empty() {
                        output += "auto& __jakt_match_value = __jakt_enum_value.as."
                        output += enum_.variants[variant_index].name()
                        output += ";\n"

                        for arg in args {
                            output += "auto& "
                            output += arg.binding
                            output += " = __jakt_match_value."
                            output += arg.name ?? "value"
                            output += ";\n"
                        }
                    }

                    for default_ in defaults {
                        output += .codegen_statement(statement: default_)
                    }

                    output += .codegen_match_body(body, return_type_id)
                    output += "}\n"
                }
                CatchAll(has_arguments, body, marker_span) => {
                    if has_arguments {
                        panic("Bindings should not be present in a generic else")
                    }

                    // TODO: Use default statement if all values are constant
                    has_default = true

                    if first {
                        output += "{"
                    } else {
                        output += "else {\n"
                    }

                    for default_ in defaults {
                        output += .codegen_statement(statement: default_)
                    }

                    output += .codegen_match_body(body, return_type_id)
                    output += "}\n"
                }
                Expression(expression, body, marker_span) => {
                    // TODO: Use case statement if all values are constant
                    if not first {
                        output += "else "
                    }
                    if expression is Range(from, to) {
                        output += "if (__jakt_enum_value"
                        if from.has_value() {
                            output += " >= "
                            output += .codegen_expression(from!)
                        }

                        if to.has_value() {
                            if from.has_value() {
                                output += "&& __jakt_enum_value "
                            }
                            output += "< "
                            output += .codegen_expression(to!)
                        }
                    } else {
                        output += "if (__jakt_enum_value == "
                        output += .codegen_expression(expression)
                    }
                    output += ") {\n"
                    output += .codegen_match_body(body, return_type_id)
                    output += "}\n"
                }
                ClassInstance(type, body, rebind_name, marker_span) => {
                    let type_name = .codegen_type_possibly_as_namespace(type_id: type, as_namespace: true)
                    output += format("if (is<{}>(__jakt_enum_value.ptr())) {{\n", type_name)
                    output += format(
                        "auto {} = NonnullRefPtr {{ *static_cast<RawPtr<{}>>(__jakt_enum_value.ptr()) }};\n"
                        (rebind_name?.name ?? "__jakt_match_value")
                        type_name
                    )

                    for default_ in defaults {
                        output += .codegen_statement(statement: default_)
                    }

                    output += .codegen_match_body(body, return_type_id)
                    output += "}\n"
                }
            }
            first = false
        }
        if return_type_id == void_type_id() or return_type_id == unknown_type_id() {
            output += "return JaktInternal::ExplicitValue<void>();\n"
        } else if not has_default {
            output += "VERIFY_NOT_REACHED();\n"
        }

        output += "}())"

        return .control_flow_state.apply_control_flow_macro(
            output,
            func_return_type: .current_function!.return_type_id,
            func_can_throw: .current_function!.can_throw,
            cpp_match_result_type
        )
    }

    fn codegen_enum_match(mut this, enum_: CheckedEnum, expr: CheckedExpression, match_cases: [CheckedMatchCase], type_id: TypeId, all_variants_constant: bool) throws -> String {
        mut output = ""

        let subject = .codegen_expression(expr)
        let needs_deref = enum_.is_boxed and subject != "*this"

        let cpp_match_result_type = .codegen_type(type_id)

        if enum_.underlying_type_id == void_type_id() {
            output += "([&]() -> JaktInternal::ExplicitValueOrControlFlow<"
            output += cpp_match_result_type
            output += ", "
            output += .codegen_function_return_type(function: .current_function!)
            output += ">{\n"
            output += "auto&& __jakt_match_variant = "
            if needs_deref {
                output += "*"
            }
            output += .codegen_expression(expr) + ";\n"
            output += "switch(__jakt_match_variant.__jakt_init_index()) {\n"

            mut has_default = false
            for match_case in match_cases {
                match match_case {
                    EnumVariant(name, args, subject_type_id, index, scope_id, body) => {
                        let enum_type = .program.get_type(subject_type_id)
                        let enum_id = match enum_type {
                            Enum(id) => id
                            else => {
                                panic("Expected enum type")
                            }
                        }
                        let match_case_enum = .program.get_enum(enum_id)
                        let variant = match_case_enum.variants[index]
                        output += format("case {} /* {} */: ", index, variant.name()) + "{\n"
                        match variant {
                            Untyped(name) => {
                            }
                            Typed(name, type_id) => {
                                if not args.is_empty() {
                                    output += format(
                                        "auto&& __jakt_match_value = __jakt_match_variant.as.{};",
                                        name
                                    )

                                    let arg = args[0]
                                    let var = .program.find_var_in_scope(scope_id, var: arg.binding)!
                                    output += .codegen_type(var.type_id)
                                    if not var.is_mutable {
                                        output += " const"
                                    }
                                    output += "& "
                                    output += arg.binding
                                    output += " = __jakt_match_value.value;\n"
                                }
                            }
                            StructLike(name, fields) => {
                                if not args.is_empty() {
                                    output += format(
                                        "auto&& __jakt_match_value = __jakt_match_variant.as.{};",
                                        name)

                                    for arg in args {
                                        let var = .program.find_var_in_scope(scope_id, var: arg.binding)!
                                        output += .codegen_type(var.type_id)
                                        if not var.is_mutable {
                                            output += " const"
                                        }
                                        output += "& "
                                        output += arg.binding
                                        output += " = __jakt_match_value."
                                        output += arg.name.value_or(arg.binding)
                                        output += ";\n"
                                    }
                                }
                            }
                            else => {
                                todo(format("codegen_enum_match match variant else: {}", variant))
                            }
                        }

                        for default_ in match_case.defaults {
                            output += .codegen_statement(statement: default_)
                        }

                        output += .codegen_match_body(body, return_type_id: type_id)
                        output += "};/*case end*/\n"
                    }
                    CatchAll(body) => {
                        has_default = true

                        output += "default: {\n"
                        for default_ in match_case.defaults {
                            output += .codegen_statement(statement: default_)
                        }

                        output += .codegen_match_body(body, return_type_id: type_id)
                        output += "};/*case end*/\n"
                    }
                    else => {
                        panic("Matching enum subject with non-enum value")
                    }
                }
            }
            if not has_default {
                if enum_.variants.size() != match_cases.size() {
                    panic("Inexhaustive match statement")
                }
                output += "default: VERIFY_NOT_REACHED();"
            }
            output += "}/*switch end*/\n"
            output += "}()\n)"
        } else {
            output += "([&]() -> JaktInternal::ExplicitValueOrControlFlow<"
            output += cpp_match_result_type
            output += ", "
            output += .codegen_function_return_type(function: .current_function!)
            output += ">{\n"
            output += "switch (" + .codegen_expression(expr) + ") {\n"

            for match_case in match_cases {
                match match_case {
                    EnumVariant(name, body) => {
                        output += "case " + enum_.name + "::" + name + ": {\n"
                        output += .codegen_match_body(body, return_type_id: type_id)
                        output += "}\n"
                    }
                    CatchAll(body) => {
                        output += "default: {\n"
                        output += .codegen_match_body(body, return_type_id: type_id)
                        output += "}\n"
                    }
                    else => {
                        todo(format("underlying type enum match, match_case: {}", match_case))
                    }
                }
            }
            output += "}/*switch end*/\n"
            output += "}()\n)"
        }

        return .control_flow_state.apply_control_flow_macro(
            output,
            func_return_type: .current_function!.return_type_id,
            func_can_throw: .current_function!.can_throw,
            cpp_match_result_type
        )
    }

    fn codegen_match_body(mut this, body: CheckedMatchBody, return_type_id: TypeId) throws -> String {
        mut output = ""

        match body {
            Block(block) => {
                output += .codegen_block(block)

                if return_type_id == void_type_id() or return_type_id == unknown_type_id() {
                    output += "return JaktInternal::ExplicitValue<void>();\n"
                }
            }
            Expression(expr) => {
                if expr.type() == void_type_id() or expr.type() == never_type_id() or (
                    expr.type() == unknown_type_id() and
                    not expr is OptionalNone
                ) {

                    output += "return ("
                    output += .codegen_expression(expr)
                    output += "), JaktInternal::ExplicitValue<void>();\n"
                } else {
                    output += "return JaktInternal::ExplicitValue("
                    output += .codegen_expression(expr)
                    output += ");\n"
                }
            }
        }
        return output
    }

    fn codegen_function_return_type(mut this, function: CheckedFunction) throws -> String {
        if function.is_static() and function.name_for_codegen().as_name_for_use() == "main" {
            return "ErrorOr<int>"
        }
        let type_name = .codegen_type(function.return_type_id)
        if function.can_throw {
            return format("ErrorOr<{}>", type_name)
        }
        return type_name
    }

    fn codegen_binary_expression(
        mut this
        expression: CheckedExpression
        type_id: TypeId
        lhs: CheckedExpression
        rhs: CheckedExpression
        op: CheckedBinaryOperator
    ) throws -> String {
        if op.trait_implementation is Some(implementation) {
            return .codegen_method_call(
                expr: lhs
                call: implementation.call_expression
                is_optional: false
            )
        }

        if op.op is NoneCoalescing {
            let rhs_type_id = rhs.type()
            let rhs_type = .program.get_type(rhs_type_id)
            let rhs_can_throw = rhs.can_throw()

            mut output = ""

            if rhs_can_throw {
                output += .current_error_handler()
                output += "(("
            }

            output += .codegen_expression(lhs)
            if rhs_type is GenericInstance(id) and .program.get_struct(id).name_for_codegen().as_name_for_definition() == "Optional" {
                if rhs_can_throw {
                    output += ".try_value_or_lazy_evaluated_optional";
                } else {
                    output += ".value_or_lazy_evaluated_optional";
                }
            } else {
                if rhs_can_throw {
                    output += ".try_value_or_lazy_evaluated"
                } else {
                    output += ".value_or_lazy_evaluated"
                }
            }

            if rhs_can_throw {
                output += "([&]() -> ErrorOr<"
                output += .codegen_type(rhs_type_id)
                output += "> { return "
            } else {
                output += "([&] { return "
            }


            output += .codegen_expression(rhs)
            output += "; })"

            if rhs_can_throw {
                output += "))"
            }

            return output
        }

        if op.op is NoneCoalescingAssign {
            let var = .fresh_var()
            return format(
                "({{ auto&& {0} = {1}; if (!{0}.has_value()) {0} = {2}; }})"
                var
                .codegen_expression(lhs)
                .codegen_expression(rhs)
            )
        }

        if op.op is ArithmeticRightShift {
            mut output = "JaktInternal::arithmetic_shift_right("
            output += .codegen_expression(lhs)
            output += ","
            output += .codegen_expression(rhs)
            output += ")"
            return output
        }

        if op.op is Assign and lhs is IndexedDictionary(expr, index) {
            return format(
                "{}({}.set({}, {}))"
                .current_error_handler()
                .codegen_expression(expr)
                .codegen_expression(index)
                .codegen_expression(rhs)
            )
        }

        if .program.is_integer(type_id) {
            // Integer arithmetic is checked by default.
            match op.op {
                Add | Subtract | Multiply | Divide | Modulo => {
                    if .compiler.optimize {
                        return "(" + .codegen_unchecked_binary_op(lhs, rhs, op.op, type_id) + ")"
                    } else {
                        return "(" + .codegen_checked_binary_op(lhs, rhs, op.op, type_id) + ")"
                    }
                }
                AddAssign | SubtractAssign | MultiplyAssign | DivideAssign | ModuloAssign => {
                    if .compiler.optimize {
                        return "(" + .codegen_unchecked_binary_op_assignment(lhs, rhs, op.op, type_id) + ")"
                    } else {
                        return "(" + .codegen_checked_binary_op_assignment(lhs, rhs, op.op, type_id) + ")"
                    }
                }
                else => { }
            }
        }

        mut output = "("

        output += .codegen_expression(lhs)
        output += match op.op {
            Add => " + "
            Subtract => " - "
            Multiply => " * "
            Modulo => " % "
            Divide => " / "
            Assign => " = "
            AddAssign => " += "
            SubtractAssign => " -= "
            MultiplyAssign => " *= "
            ModuloAssign => " %= "
            DivideAssign => " /= "
            BitwiseAndAssign => " &= "
            BitwiseOrAssign => " |= "
            BitwiseXorAssign => " ^= "
            BitwiseLeftShiftAssign => " <<= "
            BitwiseRightShiftAssign => " >>= "
            Equal => " == "
            NotEqual => " != "
            LessThan => " < "
            LessThanOrEqual => " <= "
            GreaterThan => " > "
            GreaterThanOrEqual => " >= "
            LogicalAnd => " && "
            LogicalOr => " || "
            BitwiseAnd => " & "
            BitwiseOr => " | "
            BitwiseXor => " ^ "
            ArithmeticLeftShift => " << "
            BitwiseLeftShift => " << "
            BitwiseRightShift => " >> "
            else => {
                todo(format("codegen_binary_expression {}", op))
            }
        }
        output += .codegen_expression(rhs)

        output += ")"
        return output
    }

    fn codegen_unchecked_binary_op(mut this, lhs: CheckedExpression, rhs: CheckedExpression, anon op: BinaryOperator, type_id: TypeId) throws -> String {
        mut output = "static_cast<"
        output += .codegen_type(type_id)
        output += ">("
        output += .codegen_expression(lhs)
        output += match op {
            Add => " + "
            Subtract => " - "
            Multiply => " * "
            Divide => " / "
            Modulo => " % "
            else => {
                panic(format("Checked binary operation codegen is not supported for BinaryOperator::{}", op))
            }
        }
        output += .codegen_expression(rhs)
        output += ")"
        return output
    }

    fn codegen_checked_binary_op(mut this, lhs: CheckedExpression, rhs: CheckedExpression, anon op: BinaryOperator, type_id: TypeId) throws -> String {
        mut output = ""
        output += "JaktInternal::"

        output += match op {
            Add => "checked_add"
            Subtract => "checked_sub"
            Multiply => "checked_mul"
            Divide => "checked_div"
            Modulo => "checked_mod"
            else => {
                panic(format("Checked binary operation codegen is not supported for BinaryOperator::{}", op))
            }
        }

        output += "<"
        output += .codegen_type(type_id)
        output += ">("
        output += .codegen_expression(lhs)
        output += ","
        output += .codegen_expression(rhs)
        output += ")"

        return output
    }

    fn codegen_unchecked_binary_op_assignment(mut this, lhs: CheckedExpression, rhs: CheckedExpression, anon op: BinaryOperator, type_id: TypeId) throws -> String {
        mut output = ""

        output += "{"
        output += "auto& _jakt_ref = "
        output += .codegen_expression(lhs)
        output += ";"
        output += "_jakt_ref = static_cast<"
        output += .codegen_type(type_id)
        output += ">(_jakt_ref "
        output += match op {
            AddAssign => " + "
            SubtractAssign => " - "
            MultiplyAssign => " * "
            DivideAssign => " / "
            ModuloAssign => " % "
            else => {
                panic(format("Checked binary operation assignment codegen is not supported for BinaryOperator::{}", op))
            }
        }
        output += .codegen_expression(rhs)
        output += ");"
        output += "}"
        return output
    }

    fn codegen_checked_binary_op_assignment(mut this, lhs: CheckedExpression, rhs: CheckedExpression, anon op: BinaryOperator, type_id: TypeId) throws -> String {
        mut output = ""

        output += "{"
        output += "auto& _jakt_ref = "
        output += .codegen_expression(lhs)
        output += ";"
        output += "_jakt_ref = JaktInternal::"
        output += match op {
            AddAssign => "checked_add"
            SubtractAssign => "checked_sub"
            MultiplyAssign => "checked_mul"
            DivideAssign => "checked_div"
            ModuloAssign => "checked_mod"
            else => {
                panic(format("Checked binary operation assignment codegen is not supported for BinaryOperator::{}", op))
            }
        }
        output += "<"
        output += .codegen_type(type_id)
        output += ">(_jakt_ref, "
        output += .codegen_expression(rhs)
        output += ");"
        output += "}"
        return output
    }

    fn codegen_method_call(mut this, expr: CheckedExpression, call: CheckedCall, is_optional: bool) throws -> String {
        mut output = ""
        if call.callee_throws {
            output += .current_error_handler()
            output += "(("
        }

        let object = .codegen_expression_and_deref_if_generic_and_needed(expr)

        if call.function_id.has_value() and (call.force_inline is ForceInline) {
            let function = .program.get_function(call.function_id!)
            let is_mutable = expr.is_mutable(program: .program) and function.params.first()!.variable.is_mutable

            let reference_type = match is_mutable {
                true => Type::MutableReference(expr.type())
                false => Type::Reference(expr.type())
            }
            let var = CheckedVariable(
                name: "self"
                type_id: .program.find_or_add_type_id(
                    reference_type
                    module_id: expr.type().module
                )
                is_mutable
                definition_span: expr.span()
                type_span: None
                visibility: CheckedVisibility::Public
            )

            mut params: [CheckedParameter] = [
                CheckedParameter(
                    requires_label: false
                    variable: var
                    default_value_expression: None
                )
            ]
            for param in function.params[1..] {
                params.push(param)
            }

            if function.block.statements.is_empty() {
                eprintln("Shit, block is empty in function ({}): {}", call.function_id!, function)
                abort()
            }

            let lambda = CheckedExpression::Function(
                captures: [],
                params,
                can_throw: function.can_throw,
                return_type_id: function.return_type_id,
                block: function.block,
                span: expr.span(),
                type_id: unknown_type_id(),
                pseudo_function_id: call.function_id,
                scope_id: function.function_scope_id,
            )

            let old_this_replacement = .this_replacement
            .this_replacement = "self"

            output += .codegen_expression(lambda)

            .this_replacement = old_this_replacement

            output += "("
            output += object
            for (_, arg) in call.args {
                output += ","
                output += .codegen_expression(arg)
            }
            output += ")"
            if call.callee_throws {
                output += "))"
            }

            return output
        }

        let expression_type = .program.get_type(expr.type())
        let name = call.name_for_codegen()

        let field_accessor = match name {
            Operator => ""
            else => match expression_type {
                RawPtr => "->"
                Struct(id) | GenericInstance(id) => match .program.get_struct(id).record_type is Class and object != "*this" {
                    true => "->"
                    false => "."
                }
                Enum(id) => match .program.get_enum(id).is_boxed and object != "*this" {
                    true => "->"
                    false => "."
                }
                else => match expression_type.is_builtin() and not expression_type is JaktString {
                    true => ""
                    false => "."
                }
            }
        }

        let generate_method_name = fn[this, &call, &name, &output]() throws {
            if name is Operator {
                output += name.as_name_for_use()
                return
            }

            let generic_parameters = call.type_args
            if not generic_parameters.is_empty() {
                output += "template "
            }

            output += name.as_name_for_use()

            if not generic_parameters.is_empty() {
                mut types: [String] = []
                for gen_param in generic_parameters {
                    types.push(.codegen_type_possibly_as_namespace(type_id: gen_param, as_namespace: false))
                }
                output += format("<{}>", join(types, separator: ", "))
            }
        }

        let is_called_as_method = not name.is_prefix() and (not field_accessor.is_empty() or (not is_optional and name is Operator))

        if is_called_as_method {
            output += "(("
            output += object
            output += ")"
            output += field_accessor
        } else {
            // object.method(...) -> method(object, ...)
            generate_method_name()
            output += "("
            output += object
        }

        if is_optional {
            // Note: `object' cannot be a builtin here as its type is Optional<T>.
            output += "map([&](auto& _value) { return _value"
            mut access_operator = "."
            if expression_type is GenericInstance(args) and args.size() > 0 {
                match .program.get_type(args[0]) {
                    Struct(id) | GenericInstance(id) => {
                        if .program.get_struct(id).record_type is Class {
                            access_operator = "->"
                        }
                    }
                    else => {}
                }
            }
            output += access_operator
        }

        if is_called_as_method {
            generate_method_name()
            output += "("
        }

        mut first = is_called_as_method
        for (_, expr) in call.args {
            if first {
                first = false
            } else {
                output += ","
            }
            output += .codegen_expression(expr)
        }

        if is_called_as_method {
            output += ")"
        }

        if is_optional {
            output += "; })"
        }
        output += ")"

        if call.callee_throws {
            output += "))"
        }
        return output
    }

    fn codegen_call(mut this, call: CheckedCall) throws -> String {
        mut output = ""

        if call.function_id.has_value() and .program.get_function(call.function_id!).is_comptime {
            return format(
                "fail_comptime_call<{}, \"Comptime function {} called outside Jakt compiler\">()"
                .codegen_type(call.return_type)
                call.name
            )
        }

        if call.callee_throws {
            output += .current_error_handler()
            output += "(("
        }
        match call.name {
            "print" | "println" | "eprintln" | "eprint" | "format" => {
                let helper = match call.name {
                    "print" => "out"
                    "println" => "outln"
                    "eprint" => "warn"
                    "eprintln" => "warnln"
                    "format" => "__jakt_format"
                    else => ""
                }
                output += helper
                output += "("
                for i in 0..call.args.size() {
                    let (_, expr) = call.args[i]
                    output += .codegen_expression(expr)
                    if i != call.args.size() - 1 {
                        output += ","
                    }
                }
                output += ")"
            }
            else => {
                mut close_enum_type_wrapper = false
                if call.function_id.has_value() {
                    let function_id = call.function_id!
                    let function = .program.get_function(function_id)
                    let type_module = .program.get_module(function_id.module)

                    if function.type is ImplicitConstructor or function.type is ExternalClassConstructor {
                        let type_id = call.return_type
                        let type = .program.get_type(type_id)
                        output += .codegen_namespace_path(call)

                        match type {
                            Struct(struct_id) => {
                                let struct_ = .program.get_struct(struct_id)
                                if struct_.record_type is Class {
                                    output += call.name_for_codegen().as_name_for_use()
                                    output += "::__jakt_create"
                                } else {
                                    output += call.name_for_codegen().as_name_for_use()
                                }
                            }
                            GenericInstance(id, args) => {
                                let struct_ = .program.get_struct(id)
                                if struct_.record_type is Class {
                                    output += struct_.name_for_codegen().as_name_for_use()
                                    output += "<"
                                    mut first = true
                                    for arg in args {
                                        if not first {
                                            output += ", "
                                        } else {
                                            first = false
                                        }
                                        output += .codegen_type(arg)
                                    }
                                    output += ">::__jakt_create"
                                } else {
                                    output += call.name_for_codegen().as_name_for_use()
                                    output += "<"
                                    mut first = true
                                    for arg in args {
                                        if not first {
                                            output += ", "
                                        } else {
                                            first = false
                                        }
                                        output += .codegen_type(arg)
                                    }
                                    output += ">"
                                }
                            }
                            else => {
                                panic("Should be unreachable")
                            }
                        }
                    } else if function.type is ImplicitEnumConstructor {
                        match .program.get_type(function.return_type_id) {
                            Enum(enum_id) => {
                                let enum_ = .program.get_enum(enum_id)
                                let enum_type_module = .program.get_module(enum_id.module)

                                output += .codegen_type_possibly_as_namespace(type_id: call.return_type, as_namespace: true)
                                output += "::" + call.name_for_codegen().as_name_for_use()
                            }
                            GenericEnumInstance(id) => {
                                todo("codegen generic enum instance")
                            }
                            else => {
                                panic("constructor expected enum type")
                            }
                        }
                    } else {
                        output += .codegen_namespace_path(call)
                        output += call.name_for_codegen().as_name_for_use()
                    }
                } else {
                    output += .codegen_namespace_path(call)
                    output += call.name_for_codegen().as_name_for_use()
                }

                let generic_parameters = call.type_args

                if not generic_parameters.is_empty() {
                    mut types: [String] = []
                    for gen_param in generic_parameters {
                        types.push(.codegen_type_possibly_as_namespace(type_id: gen_param, as_namespace: false))
                    }
                    output += format("<{}>", join(types, separator: ", "))
                }

                mut arguments: [String] = []
                for arg in call.args {
                    arguments.push(.codegen_expression(arg.1))
                }

                output += format("({})", join(arguments, separator: ","))
            }
        }

        if call.callee_throws {
            output += "))"
        }

        return output
    }

    fn codegen_namespace_path(mut this, call: CheckedCall) throws -> String {
        if call.function_id.has_value() {
            let func = .program.get_function(call.function_id!)
            if func.owner_scope.has_value() {
                return .codegen_namespace_qualifier(
                    scope_id: func.owner_scope!
                    skip_current: false
                    possible_constructor_name: call.name_for_codegen().as_name_for_use()
                    generic_mappings: func.owner_scope_generics
                )
            }
        }

        mut output = ""
        mut index: usize = 0

        for namespace_ in call.namespace_ {
            // hack warning: this is to get around C++'s limitation that a constructor
            // can't be called like other static methods
            if index == call.namespace_.size() - 1 and namespace_.name == call.name_for_codegen().as_name_for_use() {
                break
            }

            output += namespace_.name_for_codegen().as_name_for_use()
            if namespace_.generic_parameters.has_value() {
                output += "<"
                mut i: usize = 0
                for param in namespace_.generic_parameters! {
                    output += .codegen_type(param)
                    if i != namespace_.generic_parameters!.size() - 1 {
                        output += ","
                    }
                    ++i
                }
                output += ">"
            }
            output += "::"

            ++index
        }
        return output
    }

    fn codegen_block(mut this, block: CheckedBlock) throws -> String {
        mut output = ""

        if block.yielded_type.has_value() {
            let yielded_type = block.yielded_type!
            let type_output = .codegen_type(yielded_type)
            let fresh_var = .fresh_var()
            let fresh_label = .fresh_label()

            .entered_yieldable_blocks.push((fresh_var, fresh_label))

            output += "({ Optional<"
            output += type_output
            output += "> "
            output += fresh_var
            output += "; "
        }

        output += "{\n"

        for statement in block.statements {
            output += .codegen_statement(statement)
        }

        output += "}\n"

        if block.yielded_type.has_value() {
            let (var, label) = .entered_yieldable_blocks.pop()!

            output += label
            output += ":; "
            output += var
            if not block.yielded_none {
                output += ".release_value()"
            }
            output += "; })"
        }

        return output
    }

    fn codegen_statement(mut this, statement: CheckedStatement) throws -> String {
        mut add_newline = true
        mut output = ""
        if .debug_info.statement_span_comments and statement.span().has_value() and add_newline {
            output += format("\n#line {}\n", .debug_info.span_to_source_location(statement.span()!))
        }

        output += match statement {
            Throw(expr) => "return " + .codegen_expression(expr) + ";"
            Continue => match .control_flow_state.passes_through_match {
                true => "return JaktInternal::LoopContinue{};"
                false => "continue;"
            }
            Break => match .control_flow_state.passes_through_match {
                true => "return JaktInternal::LoopBreak{};"
                false => "break;"
            }
            Expression(expr) => .codegen_expression(expr) + ";"
            Defer(statement) => {
                mut output = ""
                output += "ScopeGuard "
                output += .fresh_var()
                output += "([&] {\n"
                let last_control_flow = .control_flow_state
                let old_inside_defer = .inside_defer

                .control_flow_state.passes_through_match = false
                .inside_defer = true

                output += .codegen_statement(statement)
                output += "});"

                .control_flow_state = last_control_flow
                .inside_defer = old_inside_defer
                yield output
            }
            Return(val) => match val.has_value() {
                true => match .current_function!.can_throw {
                    true => {
                        let type = .program.get_type(val!.type())
                        mut result = ""
                        if type is Void or type is Never {
                            result = .codegen_expression(val!) + "; return {}"
                        } else {
                            result = "return " + .codegen_expression(val!)
                        }

                        yield result + ";"
                    }
                    false => "return " + .codegen_expression(val!) + ";"
                }
                false => {
                    yield match .current_function!.can_throw {
                        true => "return {};"
                        false => "return;"
                    }
                }
            }
            Loop(block) => {
                mut output = ""
                if .debug_info.statement_span_comments and statement.span().has_value() {
                    output += format("\n#line {}\n", .debug_info.span_to_source_location(statement.span()!))
                }
                output += "for (;;)"
                add_newline = false
                let last_control_flow = .control_flow_state
                .control_flow_state = last_control_flow.enter_loop()
                let block_str = .codegen_block(block)
                .control_flow_state = last_control_flow
                output += block_str
                yield output
            }
            While(condition, block) => {
                mut output = ""
                if .debug_info.statement_span_comments and statement.span().has_value() {
                    output += format("\n#line {}\n", .debug_info.span_to_source_location(statement.span()!))
                }
                output += "while ("
                output += .codegen_expression(expression: condition)
                output += ")"

                {
                    let last_control_flow = .control_flow_state
                    .control_flow_state = last_control_flow.enter_loop()
                    let code = .codegen_block(block)
                    .control_flow_state = last_control_flow
                    output += code
                }
                add_newline = false
                yield output
            }
            Block(block) => .codegen_block(block)
            Garbage => {
                panic("Garbage statement in codegen")
            }
            DestructuringAssignment(vars, var_decl) => {
                mut output = ""
                output += .codegen_statement(statement: var_decl)

                for v in vars {
                    output += .codegen_statement(statement: v)
                }
                yield output
            }
            VarDecl(var_id, init) => {
                let var = .program.get_variable(var_id)

                mut output = ""
                let var_type = .program.get_type(var.type_id)
                output += .codegen_type(var.type_id)
                output += " "
                if not var.is_mutable and not (var_type is Reference or var_type is MutableReference) {
                    output += "const "
                }
                output += var.name_for_codegen().as_name_for_use()
                output += " = "
                output += .codegen_expression(init)
                output += ";"
                yield output
            }
            InlineCpp(lines) => {
                mut output = ""
                for line in lines {
                    mut escaped_line = line
                    escaped_line = escaped_line.replace(replace: "\\\"", with: "\"")
                    escaped_line = escaped_line.replace(replace: "\\\\", with: "\\")
                    output += escaped_line
                }
                yield output
            }
            If(condition, then_block, else_statement) => {
                mut output = ""

                if .debug_info.statement_span_comments and statement.span().has_value() {
                    output += format("\n#line {}\n", .debug_info.span_to_source_location(statement.span()!))
                }
                output += "if ("
                output += .codegen_expression(condition)
                output += ")"

                output += .codegen_block(block: then_block)

                output += match else_statement.has_value() {
                    true => "else " + .codegen_statement(statement: else_statement!)
                    false => ""
                }

                add_newline = false

                yield output
            }
            Yield(expr, span) => {
                mut output = ""

                if .entered_yieldable_blocks.size() == 0 {
                    panic("Must be in a block to yield")
                }

                let (var_name, end_label) = .entered_yieldable_blocks.last()!

                output += var_name
                output += " = "
                output += .codegen_expression(expr)
                output += "; goto "
                output += end_label
                output += ";\n"

                yield output
            }
        }

        if add_newline {
            output += "\n"
        }

        return output
    }


    fn codegen_type(mut this, anon type_id: TypeId) throws -> String {
        return .codegen_type_possibly_as_namespace(type_id, as_namespace: false)
    }

    fn codegen_type_possibly_as_namespace(mut this, type_id: TypeId, as_namespace: bool) throws -> String {
        mut qualifiers = ""

        if not as_namespace and .program.get_type(type_id).qualifiers.is_immutable {
            qualifiers += " const"
        }

        if .generic_inferences is Some(mappings) {
            if mappings.get(type_id) is Some(id) {
                return .codegen_type_possibly_as_namespace(type_id: id, as_namespace) + qualifiers
            }
        }

        return match .program.get_type(type_id) {
            Void => "void"
            Bool => "bool"
            U8 => "u8"
            U16 => "u16"
            U32 => "u32"
            U64 => "u64"
            I8 => "i8"
            I16 => "i16"
            I32 => "i32"
            I64 => "i64"
            F32 => "f32"
            F64 => "f64"
            Usize => "size_t"
            JaktString => "ByteString"
            CChar => "char"
            CInt => "int"
            Never => "void"
            Const(value) => {
                mut interpreter = Interpreter::create(
                    compiler: .compiler
                    program: .program
                    typecheck_functions: TypecheckFunctions(
                        block: fn(
                            parsed_block: ParsedBlock
                            parent_scope_id: ScopeId
                            safety_mode: SafetyMode
                            yield_type_hint: TypeId?
                            containing_function_id: FunctionId? = None
                        ) throws -> CheckedBlock {
                            throw Error::from_string_literal("Cannot typecheck a const block")
                        }

                        register_function: fn(
                            function: CheckedFunction
                        ) throws -> FunctionId {
                            throw Error::from_string_literal("Cannot typecheck a const function")
                        }
                    )
                    spans: []
                )
                let expr = value_to_checked_expression(value, interpreter)
                yield .codegen_expression(expr)
            }
            RawPtr(type_id) => match .program.get_type(type_id).is_boxed(program: .program) {
                true => .codegen_type_possibly_as_namespace(type_id, as_namespace: true) + "*"
                false => .codegen_type(type_id) + "*"
            }
            Reference(type_id) => .codegen_type(type_id) + " const&"
            MutableReference(type_id) => .codegen_type(type_id) + "&"
            GenericResolvedType(id, args) | GenericInstance(id, args) => .codegen_generic_type_instance(id, args, as_namespace)
            Struct(id) => {
                let implements_type = .program.get_struct(id).implements_type
                yield match implements_type.has_value() {
                    true => .codegen_type_possibly_as_namespace(type_id: implements_type!, as_namespace)
                    false => .codegen_struct_type(id, as_namespace)
                }
            }
            Enum(id) => .codegen_enum_type(id, as_namespace)
            GenericEnumInstance(id, args) => .codegen_generic_enum_instance(id, args, as_namespace)
            TypeVariable(name) => name
            Function(params, can_throw, return_type_id) => {
                mut output = "Function<"
                if can_throw {
                    output += "ErrorOr<"
                }
                output += .codegen_type(return_type_id)
                if can_throw {
                    output += ">"
                }
                output += "("
                mut first = true
                for param in params {
                    if first {
                        first = false
                    } else {
                        output += ", "
                    }
                    output += .codegen_type(param)
                }
                output += ")>"
                yield output
            }
            GenericTraitInstance => {
                panic("Generic trait instance in codegen")
            }
            else => {
                // The RBC uses 'auto' here but we probably want to improve in the future
                yield "auto"
            }
        } + qualifiers
    }

    fn codegen_generic_type_instance(mut this, id: StructId, args: [TypeId], as_namespace: bool) throws -> String {
        mut output = ""
        let type_module = .program.get_module(id.module)

        mut namespace_ = ""
        // For prelude generic instances prefix with "JaktInternal::" so that there aren't naming clashes
        if type_module.is_prelude() {
            namespace_ += "JaktInternal::"
        }

        let inner_weak_ptr_struct_id = .program.check_and_extract_weak_ptr(struct_id: id, args)

        if inner_weak_ptr_struct_id.has_value() {
            output += "WeakPtr<"
            output += namespace_

            let inner_struct_id = inner_weak_ptr_struct_id.value()
            let struct_ = .program.get_struct(inner_struct_id)
            output += .codegen_namespace_qualifier(scope_id: struct_.scope_id)
            output += struct_.name_for_codegen().as_name_for_use()

            output += ">"
        } else {
            let struct_ = .program.get_struct(id)

            let acquired_by_ref = not as_namespace and struct_.record_type is Class
            if acquired_by_ref {
                output += "NonnullRefPtr<"
            }
            output += namespace_
            output += .codegen_namespace_qualifier(scope_id: struct_.scope_id)
            output += struct_.name_for_codegen().as_name_for_use()
            output += "<"
            mut first = true
            for type_id in args {
                if not first {
                    output += ","
                } else {
                    first = false
                }
                output += .codegen_type(type_id)
            }
            output += ">"
            if acquired_by_ref {
                output += ">"
            }
        }

        return output
    }

    fn codegen_generic_enum_instance(mut this, id: EnumId, args: [TypeId], as_namespace: bool) throws -> String {
        mut output = ""
        mut close_tag = false
        let enum_ = .program.get_enum(id)
        if not as_namespace and enum_.is_boxed {
            output += "NonnullRefPtr<"
            let qualifier = .codegen_namespace_qualifier(scope_id: enum_.scope_id)

            if not qualifier.is_empty() {
                output += "typename "
                output += qualifier
            }
            output += enum_.name
            close_tag = true
        } else {
            let qualifier = .codegen_namespace_qualifier(scope_id: enum_.scope_id)

            if not qualifier.is_empty() {
                if not as_namespace {
                    output += "typename "
                }
                output += qualifier
            }
            output += enum_.name
        }
        output += "<"
        mut first = true
        for type_id in args {
            if not first {
                output += ", "
            } else {
                first = false
            }

            output += .codegen_type(type_id)
        }
        output += ">"
        if close_tag {
            output += ">"
        }
        return output
    }

    fn codegen_namespace_qualifier(
        mut this
        scope_id: ScopeId
        skip_current: bool = true
        possible_constructor_name: String? = None
        generic_mappings: [TypeId:TypeId]? = None
    ) throws -> String {
        mut output = ""
        mut current_scope_id = match skip_current {
            true => .program.get_scope(scope_id).parent
            false => scope_id
        }

        mut first = true
        while current_scope_id.has_value() {
            let scope = .program.get_scope(current_scope_id!)
            let name = scope.namespace_name_for_codegen()?.as_name_for_use()
            let is_constructor_call = first and possible_constructor_name == name

            defer {
                if name.has_value() {
                    first = false
                }
                current_scope_id = scope.parent
            }

            if name.has_value() {
                if is_constructor_call {
                    // Don't include the constructor name in the namespace qualifier
                    continue
                }

                let args = match scope.relevant_type_id.has_value() {
                    false => ""
                    true => {
                        let parameters = .program.get_type(scope.relevant_type_id!).generic_parameters(program: .program)
                        mut res = StringBuilder::create()

                        if parameters is Some(params) and generic_mappings is Some(mappings) and not params.is_empty() {
                            res.append("<")
                            mut first = true
                            for parameter in params {
                                let mapping = mappings.get(parameter.type_id)!
                                if .program.get_type(mapping) is TypeVariable and .map_type(mapping) == mapping {
                                    // We couldn't resolve this somehow, so just...bail.
                                    // FIXME: We should make sure this can't happen?
                                    //        Or at least error out when it does.
                                    break
                                }

                                if first {
                                    first = false
                                } else {
                                    res.append(", ")
                                }

                                res.append(.codegen_type(mapping))
                            }
                            res.append(">")
                        }

                        yield res.to_string()
                    }
                }
                output = format("{}{}::{}", name!, args, output)
            }
        }
        return output
    }

    fn map_type(this, anon type_id: TypeId) -> TypeId {
        if .generic_inferences is Some(mapping) {
            return mapping.get(type_id) ?? type_id
        }
        return type_id
    }

    fn codegen_function(mut this, anon function: CheckedFunction, as_method: bool = false) throws -> String {
        if function.is_comptime {
            return ""
        }

        return .codegen_function_in_namespace(function, containing_struct: None, as_method)
    }

    fn codegen_struct_type(mut this, id: StructId, as_namespace: bool) throws -> String {
        mut output = ""
        let type_module = .program.get_module(id.module)
        let checked_struct = .program.get_struct(id)

        if not as_namespace and checked_struct.record_type is Class {
            output += "NonnullRefPtr<"
            output += .codegen_namespace_qualifier(scope_id: checked_struct.scope_id)
            output += checked_struct.name_for_codegen().as_name_for_use()
            output += ">"
        } else {
            output += .codegen_namespace_qualifier(scope_id: checked_struct.scope_id)
            output += checked_struct.name_for_codegen().as_name_for_use()
        }

        return output
    }

    fn codegen_enum_type(mut this, id: EnumId, as_namespace: bool) throws -> String {
        mut output = ""
        let type_module = .program.get_module(id.module)
        let checked_enum = .program.get_enum(id)

        if not as_namespace and checked_enum.is_boxed {
            output += "NonnullRefPtr<"
            let qualifier = .codegen_namespace_qualifier(scope_id: checked_enum.scope_id)
            if not qualifier.is_empty() {
                output += "typename "
                output += qualifier
            }
            output += checked_enum.name
            output += ">"
        } else {
            let qualifier = .codegen_namespace_qualifier(scope_id: checked_enum.scope_id)
            if not qualifier.is_empty() {
                output += qualifier
            }
            output += checked_enum.name
        }

        return output
    }

    fn codegen_destructor_predecl(mut this, anon struct_: &CheckedStruct) throws -> String {
        mut output = ""
        output += "public:\n"
        output += "    ~" + struct_.name_for_codegen().as_name_for_definition() + "();\n"
        return output
    }

    fn codegen_destructor(mut this, anon struct_: &CheckedStruct, anon function: &CheckedFunction, is_inline: bool) throws -> String {
        mut output = ""
        let qualified_name = .codegen_type_possibly_as_namespace(type_id: struct_.type_id, as_namespace: true)

        if not is_inline and not struct_.generic_parameters.is_empty() {
            output += "template <"
            output += .codegen_template_parameter_names(struct_.generic_parameters)
            output += ">\n"
        }

        if is_inline {
            output += format("~{}()", struct_.name_for_codegen().as_name_for_definition())
        } else {
            output += format("{}::~{}()", qualified_name, struct_.name_for_codegen().as_name_for_definition())
        }

        output += .codegen_block(block: function.block)

        return output
    }

    fn codegen_constructor_predecl(mut this, anon function: CheckedFunction) throws -> String {
        let type_id = function.return_type_id
        let type_ = .program.get_type(type_id)

        guard type_ is Struct(struct_id) else {
            panic("internal error: call to a constructor, but not a struct/class type")
        }

        let structure = .program.get_struct(struct_id)

        if structure.record_type is Class {
            mut output = ""

            output += "protected:\n"

            output += format("explicit {}(", function.name_for_codegen().as_name_for_definition())
            mut first = true
            for param in function.params {
                if not first {
                    output += ", "
                } else {
                    first = false
                }

                let param_type_id = param.variable.type_id
                output += .codegen_type(param_type_id)
                output += " a_"
                output += param.variable.name_for_codegen().as_name_for_definition()
            }
            output += ");\n"

            mut class_name_with_generics = ""
            class_name_with_generics += structure.name_for_codegen().as_name_for_definition()

            first = true
            for generic_parameter in structure.generic_parameters {
                if not first {
                    class_name_with_generics += ", "
                } else {
                    class_name_with_generics += "<"
                    first = false
                }

                class_name_with_generics += .codegen_type(generic_parameter.type_id)
            }
            if not structure.generic_parameters.is_empty() {
                class_name_with_generics += ">"
            }

            output += "public:\n"
            output += format("static ErrorOr<NonnullRefPtr<{}>> __jakt_create", class_name_with_generics)
            output += "("

            first = true
            for param in function.params {
                if not first {
                    output += ", "
                } else {
                    first = false
                }

                output += .codegen_type(param.variable.type_id)
                output += " "
                output += param.variable.name_for_codegen().as_name_for_definition()
            }

            output += ");\n"
            return output
        }

        mut output = ""
        output += function.name_for_codegen().as_name_for_definition()
        output += "("

        mut first = true
        for param in function.params {
            if not first {
                output += ", "
            } else {
                first = false
            }

            output += .codegen_type(param.variable.type_id)
            output += " a_"
            output += param.variable.name_for_codegen().as_name_for_definition()
        }
        output += ");\n"

        return output
    }

    fn codegen_constructor(mut this, anon function: CheckedFunction, is_inline: bool = false) throws -> String {
        let type_id = function.return_type_id
        let type_ = .program.get_type(type_id)

        guard type_ is Struct(struct_id) else {
            panic("internal error: call to a constructor, but not a struct/class type")
        }

        let structure = .program.get_struct(struct_id)
        let qualified_name = .codegen_type_possibly_as_namespace(type_id, as_namespace: true)
        mut output = ""

        if not is_inline and not structure.generic_parameters.is_empty() {
            output += "template <"
            output += .codegen_template_parameter_names(structure.generic_parameters)
            output += ">\n"
        }


        if structure.record_type is Class or structure.record_type is Struct {
            if is_inline {
                output += function.name_for_codegen().as_name_for_definition()
                output += "("
            } else {
                output += format("{}::{}(", qualified_name, function.name_for_codegen().as_name_for_definition())
            }

            mut first = true
            for param in function.params {
                if not first {
                    output += ", "
                } else {
                    first = false
                }

                let param_type_id = param.variable.type_id
                output += .codegen_type(param_type_id)
                output += " a_"
                output += param.variable.name_for_codegen().as_name_for_definition()
            }
            output += ")"

            if not function.params.is_empty() {
                output += ": "

                mut initializers: [String] = []

                let parent_constructor_parameter_count = function.params.size() - structure.fields.size()
                if parent_constructor_parameter_count > 0 {
                    mut parent_initializer = ""
                    parent_initializer += .program.get_struct(structure.super_struct_id!).name_for_codegen().as_name_for_definition()
                    parent_initializer += "("

                    mut strings: [String] = []
                    for param in function.params[0..parent_constructor_parameter_count] {
                        strings.push("move(a_" + param.variable.name_for_codegen().as_name_for_use() + ")")
                    }
                    parent_initializer += join(strings, separator: ", ")

                    parent_initializer += ")"
                    initializers.push(parent_initializer)
                }

                for i in parent_constructor_parameter_count..function.params.size() {
                    let param = function.params[i]
                    initializers.push(param.variable.name_for_codegen().as_name_for_use() + "(move(a_" + param.variable.name_for_codegen().as_name_for_use() + "))")
                }
                output += join(initializers, separator: ", ")
            }

            output += "{}\n"

            if structure.record_type is Class {

                mut class_name_with_generics = ""
                class_name_with_generics += structure.name_for_codegen().as_name_for_definition()

                first = true
                for generic_parameter in structure.generic_parameters {
                    if not first {
                        class_name_with_generics += ", "
                    } else {
                        class_name_with_generics += "<"
                        first = false
                    }

                    class_name_with_generics += .codegen_type(generic_parameter.type_id)
                }
                if not structure.generic_parameters.is_empty() {
                    class_name_with_generics += ">"
                }

                if is_inline {
                    output += "static "
                }

                let qualified_namespace = match is_inline {
                    true => ""
                    false => qualified_name + "::"
                }
                output += format("ErrorOr<NonnullRefPtr<{}>> {}__jakt_create", class_name_with_generics, qualified_namespace)
                output += "("

                first = true
                for param in function.params {
                    if not first {
                        output += ", "
                    } else {
                        first = false
                    }

                    output += .codegen_type(param.variable.type_id)
                    output += " "
                    output += param.variable.name_for_codegen().as_name_for_definition()
                }

                output += format(") {{ auto o = {}(adopt_nonnull_ref_or_enomem(new (nothrow) {} (", .current_error_handler(), class_name_with_generics)

                first = true
                for param in function.params {
                    if not first {
                        output += ", "
                    } else {
                        first = false
                    }

                    output += "move("
                    output += param.variable.name_for_codegen().as_name_for_use()
                    output += ")"
                }

                output += "))); return o; }"
            }

            return output
        }

        if not is_inline {
            output += qualified_name
            output += "::"
        }
        output += function.name_for_codegen().as_name_for_definition()
        output += "("

        mut first = true
        for param in function.params {
            if not first {
                output += ", "
            } else {
                first = false
            }

            output += .codegen_type(param.variable.type_id)
            output += " a_"
            output += param.variable.name_for_codegen().as_name_for_definition()
        }
        output += ") "

        if not function.params.is_empty() {
            output += ":"
        }

        first = true
        for param in function.params {
            if not first {
                output += ", "
            } else {
                first = false
            }

            output += param.variable.name_for_codegen().as_name_for_use()
            output += "(move(a_"
            output += param.variable.name_for_codegen().as_name_for_use()
            output += "))"
        }

        output += "{}\n"

        return output
    }

    fn codegen_function_in_namespace(
        mut this
        function: CheckedFunction
        containing_struct: TypeId?
        as_method: bool = false
        skip_template: bool = false
        explicit_generic_instantiation: [TypeId]? = None
    ) throws -> String {
        // Extern generics need to be in the header anyways, so we can't codegen for them.
        if not function.generics.params.is_empty() {
            if function.linkage is External {
                return ""
            }
        }

        if function.force_inline is ForceInline {
            return ""
        }

        mut output = ""

        if not skip_template {
            output += .codegen_function_generic_parameters(function)
        }

        let is_main = function.name_for_codegen().as_name_for_use() == "main" and not containing_struct.has_value()

        if function.force_inline is MakeDefinitionAvailable {
            output += "__attribute__((always_inline)) inline "
        }

        if function.return_type_id == never_type_id() {
            output += "[[noreturn]] "
        }
        if is_main {
            output += "ErrorOr<int>"
        } else {
            if as_method and function.is_static() {
                output += "static "
            }
            if not function.type is Destructor {
                output += match function.can_throw {
                    true => format("ErrorOr<{}>", .codegen_type(function.return_type_id))
                    false => .codegen_type(function.return_type_id)
                }
            }
        }

        output += " "

        if is_main {
            output += "main"
        } else {
            let qualifier = match containing_struct.has_value() {
                true => .codegen_type_possibly_as_namespace(type_id: containing_struct!, as_namespace: true)
                false => ""
            }
            if not qualifier.is_empty() {
                output += qualifier
                output += "::"
            }
            if function.type is Destructor {
                guard .program.get_type(containing_struct!) is Struct(struct_id) else {
                    panic("Destructor doesn't have a containing struct")
                }
                output += "~" + .program.get_struct(struct_id).name_for_codegen().as_name_for_definition()
            } else {
                output += function.name_for_codegen().as_name_for_definition()
            }
        }

        if explicit_generic_instantiation.has_value() {
            output += "<"
            mut first = true
            for type in explicit_generic_instantiation! {
                if not first {
                    output += ", "
                } else {
                    first = false
                }
                output += .codegen_type(type)
            }
            output += ">"
        }

        output += "("

        if is_main and function.params.is_empty() {
            output += "DynamicArray<ByteString>"
        }

        mut first = true
        for param in function.params {
            let variable = param.variable
            if variable.name == "this" {
                continue
            }
            if not first {
                output += ","
            } else {
                first = false
            }
            let variable_type = .program.get_type(variable.type_id)
            output += .codegen_type(variable.type_id)
            output += " "
            if not variable.is_mutable and not (variable_type is Reference or variable_type is MutableReference) {
                output += "const "
            }
            output += variable.name_for_codegen().as_name_for_definition()
        }

        output += ")"

        if not function.is_static() and not function.is_mutating() {
            output += " const"
        }

        output += " {\n"
        if is_main {
            output += "\n
            #ifdef _WIN32
            SetConsoleOutputCP(CP_UTF8);
            // Enable buffering to prevent VS from chopping up UTF-8 byte sequences
            setvbuf(stdout, nullptr, _IOFBF, 1000);
            #endif\n"
        }
        // FIXME: Panic if function type is unknown, and this isn't `main()`

        let last_control_flow = .control_flow_state
        .control_flow_state = last_control_flow.enter_function()
        let block = .codegen_block(block: function.block)
        .control_flow_state = last_control_flow
        output += block

        if is_main {
            output += "return 0;\n"
        } else {
            if function.can_throw and function.return_type_id == builtin(BuiltinType::Void) {
                output += "return {};\n"
            }
        }

        output += "}\n"

        return output
    }

    fn codegen_lambda_block(mut this, can_throw: bool, block: CheckedBlock, return_type_id: TypeId) throws -> String {
        mut output = "{\n"

        output += .codegen_block(block)

        if can_throw and return_type_id == builtin(BuiltinType::Void) {
            output += "return {};\n"
        }

        output += "}\n"
        return output
    }
}
